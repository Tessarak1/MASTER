load intersection
load path
***allIntersections is a config, that consist exclusively of Intersection objects
omod MAP is 
    protecting INTERSECTION .
    protecting LIST-LOCATION .
    protecting PATH .
    class Map | allIntersections : Configuration, paths : PathSet .

    ***return intersection with given location.
    op returnByLocation : Configuration List-Location -> Configuration .
    op returnByLocation : Configuration PathSet -> Configuration .
    op $removeConnection : PathSet Configuration -> Configuration .
    op removeConnection : PathSet Object -> Object .
    op removeConnection : Object List-Location -> Object .
    op newNeighbor : String OidSet Configuration -> Configuration .
    op createPathSet : Configuration -> PathSet .
    op findPath : List-Location PathSet -> PathSet .
    op getIntersections : Object -> Configuration .
    op getPathSet : Object -> PathSet .
    op $newIntersection : Location Path Object Nat -> Object .
    op $newIntersectionCounter : List-Location Object Nat -> Object .
    op newIntersection : Object List-Location -> Object .
    op $addPath : Location Path PathSet -> PathSet .
    op addPath : Location Path PathSet -> PathSet .
    op outsideMap : Location PathSet -> Bool .
    op addAndRemove : Object List-Location List-Location -> Object .

    vars X X2 X3 Y Y2 Y3 : Rat .
    vars OID OID2 : String .
    var CONF : Configuration .
    var O : Object .
    vars OS OS2 : OidSet .
    vars LL LL2 : List-Location .
    var L : Location .
    var PS PS2 : PathSet .
    var AS : AttributeSet .
    var N : Nat .

    eq addAndRemove(< OID : Map | >, LL, LL2) = removeConnection(newIntersection(< OID : Map | >, LL), LL2) .
    ceq outsideMap((X,Y), {(X2,Y2) (X3,Y3)} PS) = false if between((X,Y), (X2,Y2) (X3,Y3)) =/= nil .
    eq outsideMap((X,Y), PS) = true [owise] .
    eq returnByLocation(< OID : Intersection | location : (X, Y) > CONF, (X, Y) LL) = < OID : Intersection | > returnByLocation(CONF, LL) .
    eq returnByLocation(< OID : Intersection | location : (X, Y) > < OID2 : Intersection | location : (X2, Y2) > CONF, {(X, Y) (X2, Y2)} PS) = < OID : Intersection | > < OID2 : Intersection | > returnByLocation(CONF, PS) .
    eq returnByLocation(CONF, LL) = none [owise] .
    eq $removeConnection(es, CONF) = CONF .
    eq $removeConnection({(X,Y) (X2,Y2)} PS, < OID : Intersection | location : (X,Y), neighbors : OID2 OS > < OID2 : Intersection | location : (X2,Y2), neighbors : OID OS2 > CONF) =
        $removeConnection(PS, < OID : Intersection | neighbors : OS > < OID2 : Intersection | neighbors : OS2 > CONF) .
    eq removeConnection(< OID : Map | allIntersections : CONF, paths : PS >, LL) =
        < OID : Map | allIntersections : $removeConnection(findPath(LL, PS), CONF), paths : PS \ findPath(LL, PS) > .
    eq newNeighbor(OID, none, CONF) = CONF .
    eq newNeighbor(OID, OID2 OS, < OID2 : Intersection | neighbors : OS2 > CONF) = < OID2 : Intersection | neighbors : OID OS2 > newNeighbor(OID, OS, CONF) .
    eq createPathSet(none) = es .
    eq createPathSet(< OID : Intersection | neighbors : none > CONF) = createPathSet(CONF) .
    eq createPathSet(< OID : Intersection | location : (X,Y), neighbors : OID2 OS > < OID2 : Intersection | location : (X2,Y2), neighbors : OID OS2 > CONF) =
        {(X,Y) (X2,Y2)} createPathSet(< OID : Intersection | neighbors : OS > < OID2 : Intersection | neighbors : OS2 > CONF) .
    eq findPath((X,Y) LL, es) = es .
    eq findPath(nil, PS) = es .
    ceq findPath((X,Y) LL, {(X2,Y2) (X3,Y3)} PS) = {(X2,Y2) (X3,Y3)} findPath(LL, PS) if (X,Y) inRange({(X2,Y2) (X3,Y3)}) .
    eq findPath((X,Y) LL, {(X,Y) (X2,Y2)} PS) = {(X,Y) (X2,Y2)} findPath((X,Y) LL, PS) .
    eq findPath((X,Y) LL, {(X2,Y2) (X3,Y3)} PS) = findPath(LL, {(X2,Y2) (X3,Y3)} PS) [owise] .
    eq addPath((X,Y), {(X2,Y2) (X3,Y3)}, {(X2,Y2) (X3,Y3)} PS) = {(X,Y) (X2,Y2)} {(X,Y) (X3,Y3)} PS .
    eq addPath((X,Y), {(X2,Y2) (X3,Y3)}, PS) = {(X,Y) (X2,Y2)} {(X,Y) (X3,Y3)} PS [owise] .
    eq $newIntersection((X,Y), {(X2,Y2) (X3,Y3)}, < OID : Map | allIntersections : CONF, paths : PS >, N) =
        < OID : Map | allIntersections : < string(N, 10) : Intersection | location : (X,Y), neighbors : oidSet(returnByLocation(CONF, (X2,Y2) (X3,Y3))) > 
            newNeighbor(string(N, 10), oidSet(returnByLocation(CONF, (X2,Y2) (X3,Y3))), CONF),
            paths : addPath((X,Y), {(X2,Y2) (X3,Y3)}, PS) > .
    eq $newIntersectionCounter(nil, < OID : Map | >, N) = < OID : Map | > .
    eq $newIntersectionCounter((X,Y) LL, < OID : Map | allIntersections : CONF, paths : PS >, N) =
        if (findPath((X,Y), PS) == es) or (returnByLocation(CONF, (X,Y)) =/= none)
        then
            $newIntersectionCounter(LL, < OID : Map | >, N)
        else
            $newIntersectionCounter(LL, $newIntersection((X,Y), findPath((X,Y), PS), removeConnection(< OID : Map | >, (X,Y)), N), N + 1)
        fi .
    eq newIntersection(< OID : Map | >, LL) =
        $newIntersectionCounter(LL, < OID : Map | >, 0) .

    eq getIntersections(< OID : Map | allIntersections : CONF >) = CONF .
    eq getPathSet(< OID : Map | paths : PS >) = PS .
endom