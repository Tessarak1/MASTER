load auctioneer .
load intersection .
load coordinate .

omod FIELD is
    protecting INTERSECTION .
    protecting MESSAGE .
    protecting TIMER-MSET .
    protecting AUCTIONEER .
    protecting AREA .

    sorts GrowingState Level .

    class Field | 
        growingState : GrowingState, 
        waterLevel : Level, 
        fertilizerLevel : Level, 
        waterTimer : Timer,
        fertilizerTimer : Timer,
        growTimer : Timer,
        growingArea : Area,
        reserved : Bool .

    subclass Field < PassIntersection Auctioneer .

    op $progressGrowth : Object Time -> Object .
    ops low good : -> Level [ctor] .
    ops vacant growing harvest : -> GrowingState [ctor] .
    ***Changes the field based on the task that has been completed on it.
    op taskComplete : Object Tid -> Object .
    ops activeWaterTimer activeFertilizerTimer activeGrowTimer : Object -> Timer .
    
    vars OID OID2 : Oid .
    var L L2 : Level .
    vars TIME TIME2 : Time .
    vars TR TR2 TR3 TR4 : Timer .
    var GS : GrowingState .
    var N : Nat .
    var TID : Tid .
    var TT : TaskType .
    var AUS : Set{AuctionElt} .
    var CONF : Configuration .
    var BS : Set{BidElt} .
    var TTS : Set{TaskTypeElt} .

    eq taskComplete(< OID : Field | 
                waterTimer : TR, 
                reserved : true, 
                activeTasks : wateringTask , TTS >, 
            [OID, wateringTask]) 
        = 
        < OID : Field | 
            waterLevel : good, 
            waterTimer : reset(TR), 
            reserved : false,
            activeTasks : TTS
        > .

    eq taskComplete(< OID : Field | fertilizerTimer : TR, reserved : true, activeTasks : fertilizeTask , TTS >, [OID, fertilizeTask]) = < OID : Field | fertilizerLevel : good, fertilizerTimer : reset(TR), reserved : false, activeTasks : TTS > .
    eq taskComplete(< OID : Field | growingState : vacant,  reserved : true, activeTasks : plantTask , TTS >, [OID, plantTask]) = < OID : Field | growingState : growing, reserved : false, activeTasks : TTS > .
    eq taskComplete(< OID : Field | growingState : harvest, growTimer : TR,  reserved : true, activeTasks : harvestTask , TTS >, [OID, harvestTask]) = < OID : Field | growingState : vacant, growTimer : reset(TR),  reserved : false, activeTasks : TTS > .
    eq taskComplete(< OID : Field | >, TID) = < OID : Field | > [owise] .

    ***growTimer is only decreased if fertilizerlevel and waterlevel are good.
    eq $progressGrowth(< OID : Field | 
            waterLevel : good,
            fertilizerLevel : good,
            growingState : growing,
            growTimer : TR3 >,
        TIME)
        =
        < OID : Field | growTimer : dec(TR3, TIME) > .
    eq $progressGrowth(< OID : Field | >, TIME) = < OID : Field | > [owise] .
    
    eq decTimers(< OID : Field | 
            auctions : AUS, 
            waterTimer : TR, 
            fertilizerTimer : TR2 >,
        TIME)
        =
        $progressGrowth(< OID : Field | 
            auctions : decTimers(AUS, TIME),
            waterTimer : dec(TR, TIME),
            fertilizerTimer : dec(TR2, TIME) >, TIME) .

    eq activeWaterTimer(< OID : Field | activeTasks : (wateringTask , TTS) >) = empty .
    eq activeWaterTimer(< OID : Field | waterTimer : TR >) = TR [owise] .

    eq activeFertilizerTimer(< OID : Field | activeTasks : (fertilizeTask , TTS) >) = empty .
    eq activeFertilizerTimer(< OID : Field | fertilizerTimer : TR >) = TR [owise] .

    ceq activeGrowTimer(< OID : Field | activeTasks : TTS >) = empty if (harvestTask in TTS) or (plantTask in TTS) .
    ceq activeGrowTimer(< OID : Field | waterLevel : L, fertilizerLevel : L2 >) = empty if (L == low) or (L2 == low) .
    eq activeGrowTimer(< OID : Field | growTimer : TR >) = TR [owise] .

    crl [depletedWater] :
        < OID : Field | 
            waterLevel : good, 
            waterTimer : TR >
        =>
        < OID : Field | waterLevel : low >
        if expired(TR) .

    crl [depletedFertilizer] :
        < OID : Field | 
            fertilizerLevel : good,
            fertilizerTimer : TR >
        =>
        < OID : Field | fertilizerLevel : low >
        if expired(TR) .

    crl [finishedGrowth] :
        < OID : Field | growingState : growing,
            growTimer : TR >
        =>
        < OID : Field | growingState : harvest >
        if expired(TR) .

    ***Creates the relevant auction if one does not already exist.
    crl [startWaterAuction] :
        < OID : Field | 
            waterLevel : low, 
            growingState : growing,
            activeTasks : TTS >
        CONF
        =>
        initiateAuction(
            < OID : Field | 
                activeTasks : (wateringTask , TTS) >,
            [OID, wateringTask],
            CONF)
        CONF
        if not (wateringTask in TTS) .

    crl [startFertilizerAuction] :
        < OID : Field | 
            fertilizerLevel : low,
            growingState : growing, 
            activeTasks : TTS >
        CONF
        =>
        initiateAuction(
            < OID : Field | activeTasks : fertilizeTask , TTS >, 
            [OID, fertilizeTask], 
            CONF)
        CONF
        if not (fertilizeTask in TTS) .

    crl [startHarvestAuction] :
        < OID : Field | 
            growingState : harvest,
            activeTasks : TTS >
        CONF
        =>
        initiateAuction(
            < OID : Field | activeTasks : harvestTask , TTS >, 
            [OID, harvestTask],
            CONF)
        CONF
        if not (harvestTask in TTS) .

    crl [startPlantAuction] : 
        < OID : Field | growingState : vacant, activeTasks : TTS >
        CONF
        =>
        initiateAuction(< OID : Field | activeTasks : plantTask , TTS >, [OID, plantTask], CONF)
        CONF
        if not (plantTask in TTS) .
    
    rl [rcvTaskMsg] :
        < OID : Field | >
        msg TID from OID2 to OID
        =>
        < OID : Field | > .
endom