load auctioneer .
load intersection .
omod FIELD is
    protecting INTERSECTION .
    protecting MESSAGE .
    protecting TIMER .
    protecting AUCTIONEER .

    ***states if a field is empty, is ready for harvest or is growing crops.
    sort GrowingState .
    ***states if a field needs water or fertilizer.
    sort Level .


    ***when it opens an auction first check if the task is active
    ***Task being in activeTasks means that either an auction for it is running, or a robot has accepted the task and is working to perform it.
    ***Remove from activeTasks when task is finished.
    ***Add when opening auction.
    class Field | growingState : GrowingState, waterLevel : Level, fertilizerLevel : Level, 
        waterTimer : Timer, fertilizerTimer : Timer, growTimer : Timer, growingArea : Area, reserved : Bool,
        activeTasks : TaskTypeSet .

    subclass Field < PassIntersection Auctioneer .

    ops low good : -> Level [ctor] .

    ops empty growing harvest : -> GrowingState [ctor] .
    ***Changes the field based on the task that has been completed on it.
    op taskComplete : Object Tid -> Object .
    ***Returns the time until the corresponding timer expires. If the field has an active auction for the corresponding task
    ***or a robot is working on it ratInf is returned instead to prevent stalling time infinitely.
    ops waterTime fertilizerTime growTime : Object -> Time .
    ***returns the minimum of waterTime fertilizerTime and growTime.
    op fieldTimer : Object -> TimeInf .

    vars OID OID2 : Oid .
    var L L2 : Level .
    vars T T2 : Time .
    vars TR TR2 TR3 TR4 : Timer .
    var GS : GrowingState .
    var N : Nat .
    var TT : TaskType .
    var AUS : AuctionSet .
    var CONF : Configuration .
    var BS : BidSet .
    var MC : MsgContent .
    var TTS : TaskTypeSet .

    eq taskComplete(< OID : Field | waterTimer : TR, waterLevel : low, reserved : true, activeTasks : wateringTask , TTS >, [OID, wateringTask]) = < OID : Field | waterLevel : good, waterTimer : reset(TR), reserved : false, activeTasks : TTS > .
    eq taskComplete(< OID : Field | fertilizerTimer : TR, fertilizerLevel : low,  reserved : true, activeTasks : fertilizeTask , TTS >, [OID, fertilizeTask]) = < OID : Field | fertilizerLevel : good, fertilizerTimer : reset(TR), reserved : false, activeTasks : TTS > .
    eq taskComplete(< OID : Field | growingState : empty,  reserved : true, activeTasks : plantTask , TTS >, [OID, plantTask]) = < OID : Field | growingState : growing, reserved : false, activeTasks : TTS > .
    eq taskComplete(< OID : Field | growingState : harvest, growTimer : TR,  reserved : true, activeTasks : harvestTask , TTS >, [OID, harvestTask]) = < OID : Field | growingState : empty, growTimer : reset(TR),  reserved : false, activeTasks : TTS > .

    ***growTimer is only decreased if fertilizerlevel and waterlevel are good.
    eq decreaseTimers(< OID : Field | waterLevel : good, fertilizerLevel : good, waterTimer : TR, fertilizerTimer : TR2, growingState : growing, growTimer : TR3 >, T) =
        < OID : Field | waterTimer : decrease(TR, T), fertilizerTimer : decrease(TR2, T), growTimer : decrease(TR3, T) > .
    eq decreaseTimers(< OID : Field | waterTimer : TR, fertilizerTimer : TR2 >, T) =
        < OID : Field | waterTimer : decrease(TR, T), fertilizerTimer : decrease(TR2, T) > [owise] .

    eq waterTime(< OID : Field | waterLevel : low >) = ratInf .
    eq waterTime(< OID : Field | waterTimer : [cur: T - base: T2], waterLevel : good >) = T .

    eq fertilizerTime(< OID : Field | fertilizerLevel : low >) = ratInf .
    eq fertilizerTime(< OID : Field | fertilizerTimer : [cur: T - base: T2], fertilizerLevel : good >) = T .

    ceq growTime(< OID : Field | waterLevel : L, fertilizerLevel : L2 >) = ratInf if (L == low) or (L2 == low) .
    eq growTime(< OID : Field | growingState : harvest >) = ratInf .
    eq growTime(< OID : Field | growingState : empty >) = ratInf .
    eq growTime(< OID : Field | growTimer : [cur: T - base: T2], growingState : growing, waterLevel : good, fertilizerLevel : good >) = T .

    eq fieldTimer(< OID : Field | >) = min(growTime(< OID : Field | >), min(waterTime(< OID : Field | >), fertilizerTime(< OID : Field | >))) .

    ***TODO: Auction timeout leads to either removal from activeTasks or restart of auction.

    ceq needToStartAuction(< OID : Field | growingState : harvest, activeTasks : TTS >) = true if not inSet(harvestTask, TTS) .
    ceq needToStartAuction(< OID : Field | growingState : growing, waterLevel : low, activeTasks : TTS >) = true if not inSet(wateringTask, TTS) .
    ceq needToStartAuction(< OID : Field | growingState : growing, fertilizerLevel : low, activeTasks : TTS >) = true if not inSet(fertilizeTask, TTS) .
    ceq needToStartAuction(< OID : Field | growingState : empty, activeTasks : TTS >) = true if not inSet(plantTask, TTS) .
    eq needToStartAuction(< OID : Field | >) = false [owise] .

    crl [depletedWater] :
        < OID : Field | waterLevel : good, waterTimer : TR >
        =>
        < OID : Field | waterLevel : low >
        if expired(TR) .

    crl [depletedFertilizer] :
        < OID : Field | fertilizerLevel : good, fertilizerTimer : TR >
        =>
        < OID : Field | fertilizerLevel : low >
        if expired(TR) .

    crl [finishedGrowth] :
        < OID : Field | growingState : growing, growTimer : TR >
        =>
        < OID : Field | growingState : harvest >
        if expired(TR) .

    ***Creates the relevant auction if one does not already exist.
    crl [startWaterAuction] :
        < OID : Field | waterLevel : low, growingState : growing, activeTasks : TTS >
        CONF
        =>
        initiateAuction(< OID : Field | activeTasks : wateringTask , TTS >, [OID, wateringTask], CONF)
        CONF
        if not inSet(wateringTask, TTS) .

    crl [startFertilizerAuction] :
        < OID : Field | fertilizerLevel : low, growingState : growing, activeTasks : TTS >
        CONF
        =>
        initiateAuction(< OID : Field | activeTasks : fertilizeTask , TTS >, [OID, fertilizeTask], CONF)
        CONF
        if not inSet(fertilizeTask, TTS) .

    crl [startHarvestAuction] :
        < OID : Field | growingState : harvest, activeTasks : TTS >
        CONF
        =>
        initiateAuction(< OID : Field | activeTasks : harvestTask , TTS >, [OID, harvestTask], CONF)
        CONF
        if not inSet(harvestTask, TTS) .

    crl [startPlantAuction] : 
        < OID : Field | growingState : empty, activeTasks : TTS >
        CONF
        =>
        initiateAuction(< OID : Field | activeTasks : plantTask , TTS >, [OID, plantTask], CONF)
        CONF
        if not inSet(plantTask, TTS) .
endom