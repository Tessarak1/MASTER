fmod QUADRATIC-SOLVER is
    protecting CONVERSION .

    sort QuadResult .

    subsort Rat < QuadResult .

    op noResult : -> QuadResult [ctor] .
    op _;_ : QuadResult QuadResult -> QuadResult [ctor comm assoc id: noResult] .
    
    ops $quadraticFormulaMinus $quadraticFormulaPlus : Rat Rat Rat -> Rat .
    op $quadraticFormula : QuadResult -> QuadResult .
    op quadraticFormula : Rat Rat Rat -> QuadResult .
    op minElement : QuadResult -> Rat .
    op posOnly : QuadResult -> QuadResult .

    vars R R2 R3 : Rat .
    vars QR QR2 : QuadResult .

    eq minElement(R) = R .
    eq minElement((R ; R2 ; QR)) = if R >= R2 then minElement((R2 ; QR)) else minElement((R ; QR)) fi .
    
    ceq posOnly((QR ; R ; QR2)) = posOnly((QR ; QR2)) if R < 0 .
    eq posOnly(QR) = QR [owise] .

    eq $quadraticFormulaMinus(R, R2, R3) = ((-1 * R2) - rat(sqrt(float((R2 ^ 2) - (4 * R * R3))))) / (2 * R) . 
    eq $quadraticFormulaPlus(R, R2, R3) = ((-1 * R2) + rat(sqrt(float((R2 ^ 2) - (4 * R * R3))))) / (2 * R) . 

    eq $quadraticFormula(QR) = minElement(posOnly(QR)) .

    eq quadraticFormula(R, R2, R3) = 
        if ((R2 ^ 2) - (4 * R * R3)) < 0
        then
            noResult
        else
            $quadraticFormula(($quadraticFormulaMinus(R, R2, R3) ; $quadraticFormulaPlus(R, R2, R3)))
        fi .
endfm