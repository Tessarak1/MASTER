load message .

omod AUCTIONEER is
    protecting AUCTION .
    protecting MESSENGER .

    ***BidMsg sort to differentiate from other messages when pattern matching.
    sorts BidMsgContent AuctionMsgContent .
    subsort BidMsgContent AuctionMsgContent < MsgContent .

    class Auctioneer | handledTime : Bool, auctions : AuctionSet .
    
    subclass Auctioneer < Messenger .

    ***Message content to facilitate auctions
    op bid_on_ : Cost Tid -> BidMsgContent [ctor] .
    op busy_ : Tid -> BidMsgContent [ctor] .
    ops won_ reject_ accept_ : Tid -> AuctionMsgContent [ctor] .

    ***Creates an auction for every task in the TidSet and broadcasts it within range.
    op initiateAuction : Object TidSet Configuration -> Configuration .

    ***Overloaded in subclasses to determine if theo bject needs to start auctioning a task.
    op needToStartAuction : Object -> Bool .

    ***winAuction is overloaded in robot. Determines if it accepts the task or not.
    op winAuction : Object Tid -> Configuration .

    ***Object tries an auction again.
    op retryAuction : Object Configuration Auction -> Object .

    ***Decreases the timers of the active auctions.
    op decreaseAuctionTimers : AuctionSet Time -> AuctionSet .

    vars OID OID2 : Oid .
    var R : Rat .
    var TID : Tid .
    var TIDS : TidSet .
    var AUS : AuctionSet .
    var MSG : Msg .
    var BMC : BidMsgContent .
    var TR : Timer .
    vars TIME TIME2 TIME3 : Time .
    var BS : BidSet .
    var TPT : TransporterTask .
    var WT : WorkerTask .
    var OS : OidSet .
    var CONF : Configuration .
    vars COST : Cost .

    eq decreaseAuctionTimers(noAuction, TIME) = noAuction .
    eq decreaseAuctionTimers([Task: TID [cur: TIME - base: TIME2] | BS] AUS, TIME3) = [Task: TID decrease([cur: TIME - base: TIME2], TIME3) | BS] decreaseAuctionTimers(AUS, TIME3) .

    eq initiateAuction(< OID : Auctioneer | >, none, CONF) = < OID : Auctioneer | > CONF .
    eq initiateAuction(< OID : Auctioneer | auctions : AUS >, TIDS, CONF) =
        < OID : Auctioneer | auctions  : AUS createAuction(TIDS) >
        broadcastInRange(< OID : Auctioneer | >, tidToMsgContent(TIDS), CONF)  .

    ***No winner, so restat the auction.
    eq retryAuction(< OID : Auctioneer | >, CONF, [Task: TID [cur: TIME - base: TIME2] | noBid]) =
        initiateAuction(< OID : Auctioneer | >, TID, CONF) .
    ***we have a winner, so send to that winner.
    eq retryAuction(< OID : Auctioneer | auctions : AUS >, CONF, [Task: TID [cur: TIME - base: TIME2] | BS]) =
        < OID : Auctioneer | auctions : [Task: TID [cur: TIME2 - base: TIME2] | BS] AUS >
        broadcast(OID, won TID, auctionWinner(BS, "",ratInf)) [owise] .

    ***Determines the winner of an auction when its timer runs out.
    rl [auctionTimerExpire] :
        < OID : Auctioneer | auctions : [Task: TID [cur: 0 - base: TIME] | BS] AUS >
        CONF
        =>
        retryAuction(< OID : Auctioneer | auctions : AUS >, CONF, [Task: TID [cur: 0 - base: TIME] | BS])
        CONF .

    ***Adds bid to the auction
    rl [rcvBidMsg] :
        < OID : Auctioneer | auctions : [Task: TID TR | BS] AUS >
        msg bid COST on TID from OID2 to OID
        =>
        < OID : Auctioneer | auctions : [Task: TID TR | [OID2 bids COST] BS] AUS > .

    ***Close the auction when a winner has accepted the task.
    rl [rcvAcceptMsg] :
        < OID : Auctioneer | auctions : [Task: TID TR | BS] AUS >
        msg accept TID from OID2 to OID
        =>
        < OID : Auctioneer | auctions : AUS > .

    ***If the winner of the auction has become unable to complete the task, determine another winner or retry the auction.
    rl [rcvRejectMsg] :
        < OID : Auctioneer | auctions : [Task: TID TR | [OID2 bids R] BS] AUS >
        CONF
        msg reject TID from OID2 to OID
        =>
        retryAuction(< OID : Auctioneer | auctions : AUS >, CONF, [Task: TID TR | [OID2 bids R] BS])
        CONF .
endom
