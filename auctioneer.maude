load message .
load auction .

omod AUCTIONEER is
    protecting AUCTION .
    protecting MESSENGER .

    ***Auction closes when task is accepted, a winner. Need a trigger for task being complete.
    ***TODO: Remember to implement addition and removal of corresponding activeTasks from robots too.
    class Auctioneer | auctions : Set{AuctionElt}, activeTasks : Set{TaskTypeElt} .
    
    subclass Auctioneer < Messenger .

    ***Message content to facilitate auctions
    op bid_on_ : Cost Tid -> MsgContent [ctor] .
    ops won_ : Tid -> MsgContent [ctor] .

    ***Creates an auction for every task in the TidSet and broadcasts it within range.
    op initiateAuction : Object Tid Configuration -> Configuration .

    ***winAuction is overloaded in robot. Determines if it accepts the task or not.
    op winAuction : Object Tid -> Configuration .

    ***Object tries an auction again.
    op finishedAuction : Object Configuration Auction -> Object .

    vars OID OID2 : Oid .
    var TID : Tid .
    var AUS : Set{AuctionElt} .
    var TR : Timer .
    var BS : Set{BidElt} .
    var CONF : Configuration .
    vars COST : Cost .

    eq initiateAuction(< OID : Auctioneer | auctions : AUS >, TID, CONF) =
        < OID : Auctioneer | auctions  : (AUS , createAuction(TID)) >
        broadcast(OID, TID, < OID : Auctioneer | > CONF)  .

    ***No winner, so restart the auction.
    eq finishedAuction(< OID : Auctioneer | >, CONF, [Task: TID TR | empty]) =
        initiateAuction(< OID : Auctioneer | >, TID, CONF) .

    eq finishedAuction(< OID : Auctioneer | auctions : AUS >, CONF, [Task: TID TR | BS]) =
        < OID : Auctioneer | auctions : ([Task: TID TR | BS], AUS) >
        unicast(OID, won TID, auctionWinner(BS, "",cost(ratInf))) [owise] .

    ***Determines the winner of an auction when its timer runs out.
    crl [auctionTimerExpire] :
        < OID : Auctioneer | auctions : ([Task: TID TR | BS], AUS) >
        CONF
        =>
        finishedAuction(< OID : Auctioneer | auctions : AUS >, 
            CONF, 
            [Task: TID TR | BS])
        CONF
        if expired(TR) .

    ***Adds bid to the auction
    rl [rcvBidMsgAuction] :
        < OID : Auctioneer | auctions : ([Task: TID TR | BS], AUS) >
        msg bid COST on TID from OID2 to OID
        =>
        < OID : Auctioneer | auctions : ([Task: TID TR | ([OID2 bids COST], BS)], AUS) > .
    
    ***Ignores bid if no corresponding auction exists.
    crl [rcvBidMsgNoAuction] :
        < OID : Auctioneer | auctions : AUS >
        msg bid COST on TID from OID2 to OID
        =>
        < OID : Auctioneer | auctions : AUS >
        if not auctionExists(TID, AUS) .
endom
