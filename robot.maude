load time-inf .
load pathfinder .
load message .
load map .
load field .
load obstacle .
load buildings .
load auctioneer .
load entity .
load systemVars .
load representation .

omod ROBOT is 
    protecting AUCTIONEER .
    protecting MESSAGE .
    protecting OBSTACLE .
    protecting MAP .
    protecting TIME-INF .
    protecting FIELD .
    protecting PATHFINDER .
    protecting BUILDINGS .
    protecting ENTITY .
    protecting SYSTEM-VARS .
    protecting DIRECTION .
    protecting REPRESENTATION-SET .

    sort CargoLevel .
    sorts WorkerCargo TransporterCargo Cargo .
    sorts Seed Water Fertilizer Produce .
    sorts WorkState TimedWork .
    sorts YieldInformation YieldInformationSet .
    sort BaseInformation BaseInformationSet WaitingInformation .
    sort PriorityState .

    subsorts Tid TimedWork < WorkState .

    subsort Seed Water Fertilizer < WorkerCargo .
    subsort Produce < TransporterCargo .
    subsort TransporterCargo WorkerCargo < Cargo .

    subsort YieldInformation < YieldInformationSet .
    subsort BaseInformation < BaseInformationSet .

    subsort PriorityState < MsgContent .

    class Robot |
        detectionRange : Rat,
        cargoLevel : CargoLevel,
        curCargo : Cargo,
        detected : Set{RepresentationElt},
        obstructionPoints : List-Coordinate,
        curTask : TaskStatus,
        workState : WorkState,
        negotiations : NegotiationSet, 
        yieldStatus : YieldInformationSet,
        waitingStatus : WaitingInformation .
    class Worker .
    class Transporter .
    
    subclass Worker Transporter < Robot < Entity Auctioneer .

    ops priorityIdle priorityTask priorityYielding priorityNegotiating priorityWaiting priorityWorking : -> PriorityState [ctor] .
    op priorityState : Object -> PriorityState .

    op [_-_] : Oid Coordinate -> BaseInformation [ctor] .
    op noBaseInformation : -> BaseInformation [ctor] .
    op _,_ : BaseInformationSet BaseInformationSet -> BaseInformationSet [ctor assoc comm id: noBaseInformation] .
    op noWaitingInformation : -> WaitingInformation [ctor] .
    op [_|_|_] : BaseInformationSet Timer Timer -> WaitingInformation [ctor] .
    op getWaitingTimer : WaitingInformation -> MSet{TimerElt} .
    op decreaseWaitingTimers : WaitingInformation Time -> WaitingInformation .
    op rescanWaitingOn : BaseInformationSet Configuration -> BaseInformationSet .
    op inDetectionRange : Object Configuration -> Configuration .
    op addWaiting : WaitingInformation Oid Coordinate -> WaitingInformation .

    ***When it has reached the given coordinate, the robot needs to notify Oid that it can stop yielding.
    op [Oid:_-_|_] : Oid Coordinate Timer -> YieldInformation [ctor] .
    ***Robot is waiting for another to have finished yielding
    op [Oid:_|_] : Oid Timer -> YieldInformation [ctor] .
    op noYieldInformation : -> YieldInformationSet [ctor] .    
    op _,_ : YieldInformationSet YieldInformationSet -> YieldInformationSet [ctor assoc comm id: noYieldInformation] .
    op addYieldInformation : Oid Coordinate YieldInformationSet -> YieldInformationSet .
    op addYieldInformation : Oid YieldInformationSet -> YieldInformationSet .
    op getYieldTimers : YieldInformationSet -> MSet{TimerElt} .
    op removeYieldInformation : Oid YieldInformationSet -> YieldInformationSet .
    op decreaseYieldTimers : YieldInformationSet Time -> YieldInformationSet .
    op yieldExpired : YieldInformation -> Bool .
    op getOid : YieldInformation -> Oid .

    op getPhysObjs : Configuration -> Configuration .

    op water : -> Water [ctor] .
    op fertilizer : -> Fertilizer [ctor] .
    op seed : -> Seed [ctor] .
    op produce : -> Produce [ctor] .

    op needNegotiation : Object Representation Object -> Bool .
    op startNegotiation : Object Representation Object -> Configuration .
    op $timeCheck : Object Oid Time -> Object .
    op $locationChange : Object Coordinate -> Object .
    ops $locationPrevention $speedPrevention : Object Representation -> Object .
    op crashPrevention : Object Representation -> Object .
    op safetyChecks : Object Representation Object -> Configuration .

    op detect : Configuration -> Configuration .
    op _detects_ : Object Object -> Bool .

    op remove : Oid Configuration -> Configuration .

    ops noWork : -> WorkState [ctor] .
    op [_,_,_] : Oid TaskType Time -> TimedWork [ctor] .
    ops emptyCargo full : -> CargoLevel [ctor] .
    op finishedResupply : Object TaskType -> Object .

    op getLocation : Object -> Coordinate .
    op getLocation : YieldInformation -> List-Coordinate .
    ops breaksPath $breaksPath : List-Coordinate Set{RepresentationElt} -> Bool .
    op canComplete : Object Tid -> Bool .
    op $parseTasksGL : Set{RepresentationElt} -> MSet{TidElt} .
    op parseTasksGL : Set{RepresentationElt} -> MSet{TidElt} .
    op assignTask : Object Tid -> Object .
    op calculatePathLength : Object Tid Object -> Rat .
    op prepareAuction : Object MSet{TidElt} -> Configuration .
    ops mustRejectTask : Object -> Bool .
    op startWorkGL : Object -> Object .
    op arrivedTask : Object Configuration -> Bool .

    ops needsResupply needsCPoint : Object Tid -> Bool .
    ops resupplyLocCheck cPointLocCheck : Object Tid Coordinate -> List-Coordinate .
    op performChecks : Object Tid Configuration -> List-Coordinate .
    op $planPathLocations : Object Tid Configuration -> List-Coordinate .
    op $planPath : Object List-Coordinate List-Coordinate -> List-Coordinate .
    op decreaseWorkTimer : Object Time -> Object .

    ***Path that robot must traverse to be able to complete task.
    ***Travels to resupply and/or collection station if needed
    op planPath : Object Tid Object -> List-Coordinate .

    ***Implmenetn a plan path that is only reliant on destination.
    op planPath : Object Coordinate Object -> List-Coordinate .

    ***Adds the locations in the second list that are betweens locations in the first to the first list.
    op modifyPath : List-Coordinate List-Coordinate -> List-Coordinate .

    op closestPhysObjs : Coordinate Configuration Configuration -> Configuration .

    ***Checks if a given intersection has a connecting road that is not used by the list of locations.
    ***yieldLocation's boolean parameter determines if an intersection needs to have at least three connecting roads to be considered a valid solution.
    ***This is used for when a robot needs to yield to another yielding robot.
    op $yieldLocationHelper : Coordinate Set{RoadElt} -> Coordinate .
    op yieldLocation : Object Set{RoadElt} List-Coordinate -> List-Coordinate .
    ***Creates a path to the intersection that has a valid yieldLocation.
    op yieldIntersection : PQueue Object List-Coordinate Coordinate -> List-Coordinate .
    op yieldPath : Object Object List-Coordinate -> List-Coordinate .

    op conflictYieldIntersection : PQueue Object -> Object .
    op $conflictYieldPath : Configuration Object Coordinate -> List-Coordinate .
    op conflictYieldPath : Object Object -> List-Coordinate .
    op $conflictYieldProtocol : Object Object Oid List-Coordinate -> Configuration .
    op conflictYieldProtocol : Object Object Oid List-Coordinate -> Configuration .
    ***Takes in yieldPath to determine behavior based on if one was found or not.
    op $yieldProtocol : Object Object Oid List-Coordinate -> Configuration .
    ***Calculates yieldPath and sends result as argument to $yieldProtocol.
    op yieldProtocol : Object Object Oid List-Coordinate -> Configuration .

    op __leadsTo_ : Coordinate Direction Coordinate -> Bool .
    ***Treats the coordinate as an obstacle, and calculates the additional distance required to complete the robot's task.
    op replanCost : Object Object Coordinate -> Cost .

    op compatibleTaskType : Object TaskType -> Bool .

    ***Checks if the path represented by the second list is an immediate obstruction for the first.
    op pathConflict : List-Coordinate List-Coordinate -> Bool .

    var CARGO : Cargo .
    vars CONF CONF2 : Configuration .
    vars OID OID2 OID3 OID4 : Oid .
    var OIDS : Set{OidElt} .
    vars X Y X2 Y2 X3 Y3 X4 Y4 X5 Y5 : Rat .
    vars RI RI2 : RatInf .
    var N : Nat .
    var A : Area .
    vars LCO LCO2 LCO3 : List-Coordinate .
    vars L L2 : Level .
    var GS : GrowingState .
    var TC : TransporterCargo .
    var RS : Set{RoadElt} .
    var MC : MsgContent .
    var CID : Cid .
    var IS : Set{InformationElt} .
    vars D D2 : Direction .
    vars SP SP2 : Rat .
    var DR : Rat .
    var STATE : State .
    var REP : Representation .
    var REPS : Set{RepresentationElt} .
    vars OS OS2 : Set{OidElt} .
    vars TR TR2 : Timer .
    vars TIME TIME2 : Time .
    vars TID TID2 : Tid .
    var TIDS : MSet{TidElt} .
    vars AS AS2 : AttributeSet .
    var CL : CargoLevel .
    var PS : PriorityState .
    var FUNC : FunctioningState .
    var INC : IncapacitatedState .
    var PQ : PQueue .
    var O : Object . 
    var NS : NegotiationSet .
    var COST : Cost .
    var BOOL : Bool .
    var YI : YieldInformation .
    var YIS : YieldInformationSet .

    vars TT TT2 : TaskType .
    var TS : TaskStatus .
    var TPT : TransporterTask .
    var WT : WorkerTask .
    var WS : WorkState .
    var TW : TimedWork .
    var B : Bid .    
    var AUS : Set{AuctionElt} .
    var BIS : BaseInformationSet .
    var WI : WaitingInformation .
    var TWS : TimedWorkState .

    ***repairing or removing
    ceq arrivedTask(< OID : Robot | coordinate : (X,Y),
        curTask : [OID2, fixAt (X2,Y2)] >, CONF)
        =
        true if distance((X,Y), (X2,Y2)) <= ROBOT-SIZE .

    eq arrivedTask(< OID : Robot | coordinate : (X,Y),
        curTask : [OID2, unloadTask] >,
        < OID2 : CollectionStation | coordinate : (X,Y) >
        CONF)
        =
        true .
    eq arrivedTask(< OID : Robot | coordinate : (X,Y),
        curTask : [OID2, resupplyTask] >,
        < OID2 : ResupplyStation | coordinate : (X,Y) >
        CONF)
        =
        true .
    ***resupply
    ***unload
    ***GL task.

    eq getPhysObjs(< OID : Robot | > CONF) = < OID : Robot | > getPhysObjs(CONF) .
    eq getPhysObjs(CONF) = none [owise] .

    eq addWaiting(noWaitingInformation, OID, (X,Y)) = [[OID - (X,Y)] | RESCAN-TIMER | WAITING-TIMER] .
    eq addWaiting([BIS | TR | TR2], OID, (X,Y)) = [BIS , [OID - (X,Y)] | TR | reset(TR2)] .
    eq getWaitingTimer(noWaitingInformation) = empty .
    eq getWaitingTimer([BIS | TR | TR2]) = TR TR2 .
    eq decreaseWaitingTimers(noWaitingInformation, TIME) = noWaitingInformation .
    eq decreaseWaitingTimers([BIS | TR | TR2], TIME) = [BIS | decrease(TR, TIME) | decrease(TR2, TIME)] .
    ***If a robot has moved from the last registered coordinate or moved out of detection range it is removed from waiting on.
    eq rescanWaitingOn(([OID - (X,Y)] , BIS), < OID : Robot | coordinate : (X,Y) > CONF) = [OID - (X,Y)] , rescanWaitingOn(BIS, CONF) .
    eq rescanWaitingOn(BIS, CONF) = noBaseInformation [owise] .
    eq [noBaseInformation | TR | TR2] = noWaitingInformation .

    eq getTimers(< OID : Robot | auctions : AUS, yieldStatus : YIS, waitingStatus : WI >) =
        getAuctionTimers(AUS) getYieldTimers(YIS) getWaitingTimer(WI) .

    eq priorityState(< OID : Robot | state : TWS, workState : TW >) = priorityWorking .
    eq priorityState(< OID : Robot | state : waiting, waitingStatus : [BIS | TR | TR2] >) = priorityWaiting .
    ceq priorityState(< OID : Robot | state : negotiating, negotiations : NS >) = priorityNegotiating if NS =/= none .
    ceq priorityState(< OID : Robot | state : FUNC, yieldStatus : YIS >) = priorityYielding if YIS =/= noYieldInformation [owise] .
    eq priorityState(< OID : Robot | state : FUNC, curTask : TID, yieldStatus : noYieldInformation >) = priorityTask [owise] .
    eq priorityState(< OID : Robot | state : idle, curTask : noTask, yieldStatus : noYieldInformation >) = priorityIdle .

    eq compatibleTaskType(< OID : Worker | >, WT) = true .
    eq compatibleTaskType(< OID : Transporter | >, TPT) = true .
    eq compatibleTaskType(< OID : Robot | >, TT) = false [owise] .

    eq remove(OID, < OID : Robot | > CONF) = CONF .
    eq remove(OID, CONF) = CONF [owise] .

    eq yieldExpired([Oid: OID - (X,Y) | TR]) = expired(TR) .
    eq yieldExpired([Oid: OID | TR]) = expired(TR) .

    eq finishedResupply(< OID : Worker | state : resupplying, curCargo : CARGO >, wateringTask) = < OID : Worker | curCargo : water, cargoLevel : full, state : idle > .
    eq finishedResupply(< OID : Worker | state : resupplying, curCargo : CARGO >, fertilizeTask) = < OID : Worker | curCargo : fertilizer, cargoLevel : full, state : idle > .
    eq finishedResupply(< OID : Worker | state : resupplying, curCargo : CARGO >, plantTask) = < OID : Worker | curCargo : seed, cargoLevel : full, state : idle > .
    eq finishedResupply(< OID : Worker | >, TT) = < OID : Worker | > [owise] .

    ***There is a path conflict if the paths share an intersection from different direction
    ***or if they are headed directly towards one another.
    ceq pathConflict((X,Y) (X2,Y2) LCO, (X3,Y3) (X2,Y2) LCO2) = true if setDirection((X,Y),(X2,Y2)) =/= setDirection((X3,Y3), (X2,Y2)) . 
    ceq pathConflict((X,Y) (X2,Y2) LCO, (X3,Y3) (X4,Y4) LCO2) = true if
        overlap((X,Y) (X2,Y2), (X3,Y3) (X4,Y4)) and oppositeDirections(setDirection((X,Y), (X2,Y2)), setDirection((X3,Y3), (X4,Y4))) .
    eq pathConflict(LCO, (X,Y)) = findPathPair((X,Y), LCO) =/= nil .
    eq pathConflict(LCO, LCO2) = false [owise] .

    eq inDetectionRange(< OID : Robot | coordinate : (X,Y), detectionRange : DR >, < OID2 : Locatable | coordinate : (X2, Y2) > CONF) =
        < OID2 : Locatable | > inDetectionRange(< OID : Robot | >, CONF) .
    eq inDetectionRange(< OID : Robot | >, CONF) = none [owise] .

    ***Calculates the cost of replanning a path
    eq replanCost(< OID : Robot | curTask : TID, obstructionPoints : LCO >, < OID2 : Map | >, (X,Y)) =
        if planPath(< OID : Robot | obstructionPoints : LCO (X,Y) >, TID, < OID2 : Map | >) == nil
        then
            cost(ratInf)
        else
            cost(listLength(planPath(< OID : Robot | obstructionPoints : LCO (X,Y) >, TID, < OID2 : Map | >)) -
            listLength(planPath(< OID : Robot | obstructionPoints : remove(LCO, (X,Y)) >, TID, < OID2 : Map | >)))
        fi .

    ***Don't apply safety checks if the robot is moving.
    ceq safetyChecks(< OID : Robot | state : STATE >, {OID2 : Robot | IS}, < OID3 : Map | >) = < OID : Robot | > if STATE =/= moving .
    eq safetyChecks(< OID : Robot | >, {OID2 : Robot | FUNC , IS}, < OID3 : Map | >) =
        if needNegotiation(< OID : Robot | >, {OID2 : Robot | FUNC , IS}, < OID3 : Map | >)
        then
            startNegotiation(< OID : Robot | >, {OID2 : Robot | FUNC , IS}, < OID3 : Map | >)
        else
            crashPrevention(< OID : Robot | >, {OID2 : Robot | FUNC , IS})
        fi .
    ***Crash prevention consists of changing speed and/or the remaining path if relevant.
    eq crashPrevention(< OID : Robot | coordinate : (X,Y), remainingPath : LCO >, {OID2 : CID | (X2,Y2) , IS}) = 
        if findPathPair((X2, Y2), (X,Y) LCO) =/= nil
        then
            $speedPrevention($locationPrevention(< OID : Robot | >, {OID2 : CID | (X2,Y2) , IS}), {OID2 : CID | (X2,Y2) , IS})
        else
            < OID : Robot | >
        fi .

    ***Coordinate change is only performed when detecting an immobile object.
    ***If the task is to fix the robot, then we are not waiting on it, but otherwise we are.
    ***ceq $locationPrevention(< OID : Robot | waitingStatus : OS >, {OID2 : Robot | (X2,Y2) , FUNC , IS}) = if STATE =/= moving .
    ***TODO: change flow of crashPrevention. 
    eq $locationPrevention(< OID : Robot | >, {OID2 : Robot | moving , IS}) = < OID : Robot | > .
    eq $locationPrevention(< OID : Robot | coordinate : (X,Y), remainingPath : LCO >, {OID2 : Robot | (X2,Y2) , INC , IS}) = $locationChange(< OID : Robot | remainingPath : (X,Y) LCO >, (X2,Y2)) .
    eq $locationPrevention(< OID : Robot | coordinate : (X,Y), remainingPath : LCO >, {OID2 : Obstacle | (X2,Y2)}) = $locationChange(< OID : Robot | remainingPath : (X,Y) LCO >, (X2,Y2)) .
 
    eq $locationChange(< OID : Robot | remainingPath : LCO (X,Y) (X,Y2) >, (X,Y2)) =
        if Y > Y2
        then
            < OID : Robot | remainingPath : LCO (X,Y) (X,Y2 + ROBOT-SIZE) >
        else
            < OID : Robot | remainingPath : LCO (X,Y) (X,Y2 - ROBOT-SIZE) >
        fi .
    eq $locationChange(< OID : Robot | remainingPath : LCO (X,Y) (X2,Y) >, (X2,Y)) =
        if X > X2
        then
            < OID : Robot | remainingPath : LCO (X,Y) (X2 + ROBOT-SIZE,Y) >
        else
            < OID : Robot | remainingPath : LCO (X,Y) (X2 - ROBOT-SIZE,Y) >
        fi .

    ***Only coordinate change is needed here to prevent the Robot's path from overlapping with the obstacle.
    ***Speed change is dependent on the time it would take until the robot would crash with the detected object.
    eq $speedPrevention(< OID : Robot | >, {OID2 : Obstacle | IS}) = < OID : Robot | > .
    eq $speedPrevention(< OID : Robot | coordinate : (X,Y), direction : D, speed : SP >, {OID2 : Robot | (X2,Y2) , D , detectedSpeed(SP2) , moving , IS}) =
        if SP2 >= SP
        then
            < OID : Robot | >
        else
            $timeCheck(< OID : Robot | >, OID2, distance((X,Y), (X2,Y2)) / (SP - SP2))
        fi .
    ceq $speedPrevention(< OID : Robot | coordinate : (X,Y), direction : D, speed : SP >, {OID2 : Robot | (X2, Y2) , D2 , detectedSpeed(SP2) , moving , IS}) =
        $timeCheck(< OID : Robot | >, OID2, distance((X,Y), (X2,Y2)) / (SP + SP2))
        if oppositeDirections(D, D2) .
    eq $speedPrevention(< OID : Robot | >, {OID2 : Robot | IS}) = < OID : Robot | > [owise] .

    ***Modifies the robot's speed based on the calculated time until crash.
    eq $timeCheck(< OID : Robot | >, OID2, TIME) =
        if TIME < EMERGENCY-TIME
        then
            < OID : Robot | state : idle, speed : 0 >
        else
            if TIME < SAFE-TIME 
            then
                < OID : Robot | speed : SAFE-SPEED >
            else
                < OID : Robot | >
            fi
        fi .

    ceq (X,Y) north leadsTo (X,Y2) = true if Y2 > Y .
    ceq (X,Y) south leadsTo (X,Y2) = true if Y2 < Y .
    ceq (X,Y) east leadsTo (X2,Y) = true if X2 > X .
    ceq (X,Y) west leadsTo (X2,Y) = true if X2 < X .
    eq (X,Y) D leadsTo (X2,Y2) = false [owise] .

    ceq needNegotiation(< OID : Robot | negotiations : NS >, {OID2 : Robot | IS}, < OID3 : Map | >) =
        false if inNegotiationSet(NS, OID2) .
    ***Need to start negotation if two robots move towards each other directly, without any intersection in the middle.
    ceq needNegotiation(< OID : Robot | coordinate : (X,Y), direction : D, state : moving, negotiations : NS >,
        {OID2 : Robot | (X2,Y2) , D2 , moving , IS}, < OID3 : Map | roads : ({(X3,Y3) (X4,Y4)} , RS) >) =
            true if oppositeDirections(D,D2) and (findRoad((X,Y), {(X3,Y3) (X4,Y4)}) == findRoad((X2,Y2), {(X3,Y3) (X4,Y4)}))
            and not inNegotiationSet(NS, OID2) .
    ***If a robot is moving towards a stationary robot that is in the way.
    ceq needNegotiation(< OID : Robot | coordinate : (X,Y), remainingPath : LCO, state : moving, negotiations : NS >,
        {OID2 : Robot | (X2,Y2) , STATE , IS}, < OID3 : Map | roads : RS >) =
            true if (STATE =/= moving) and (findPathPair((X2,Y2), (X,Y) LCO) =/= nil)
            and not inNegotiationSet(NS, OID2) .
    ***two robots move towards the same intersection from different paths.
    ceq needNegotiation(< OID : Robot | coordinate : (X,Y), state : moving, direction : D, negotiations : NS >,
        {OID2 : Robot | (X2,Y2) , D2 , moving , IS}, < OID3 : Map | roads : ({(X3,Y3) (X4,Y4)} , {(X4, Y4) (X5,Y5)} , RS) >) =
            true if (X,Y) inRange({(X3,Y3) (X4,Y4)}) and (X2,Y2) inRange({(X4, Y4) (X5,Y5)})
            and ((X,Y) D leadsTo (X4,Y4)) and ((X2,Y2) D2 leadsTo (X4,Y4))
            and not inNegotiationSet(NS, OID2) .
    eq needNegotiation(< OID : Robot | >, {OID2 : Robot | IS}, < OID3 : Map | >) = false [owise] .

    eq startNegotiation(< OID : Robot | coordinate : (X,Y), remainingPath : LCO, negotiations : NS >, {OID2 : Robot | (X2,Y2) , IS}, < OID3 : Map | >) =
        < OID : Robot | state : negotiating, speed : 0, negotiations : addNegotiation(NS, [OID2 | (X2,Y2) | noCost]) > 
        dly(msg requestPath & path((X,Y) LCO) & priorityState(< OID : Robot | >) from OID to OID2, MSG-DELAY) .

    eq closestPhysObjs((X,Y), CONF, none) = CONF .
    eq closestPhysObjs((X,Y), < OID : Robot | coordinate : (X2,Y2), direction : D > CONF, < OID2 : Robot | coordinate : (X3,Y3), direction : D > CONF2) =
        if distance((X,Y), (X2,Y2)) < distance((X,Y), (X3,Y3))
        then
            closestPhysObjs((X,Y), < OID : Robot | > CONF, CONF2)
        else
            closestPhysObjs((X,Y), < OID2 : Robot | > CONF, CONF2)
        fi .
    eq closestPhysObjs((X,Y), CONF, < OID : Robot | > CONF2) = closestPhysObjs((X,Y), < OID : Robot | > CONF, CONF2) .
    
    eq getLocation(< OID : Robot | coordinate : (X,Y) >) = (X,Y) .
    eq getLocation([Oid: OID - (X,Y) | TR]) = (X,Y) .
    eq getLocation(YI) = nil [owise] .

    eq getYieldTimers(([Oid: OID - (X,Y) | TR] , YIS)) = TR getYieldTimers(YIS) .
    eq getYieldTimers(([Oid: OID | TR] , YIS)) = TR getYieldTimers(YIS) .
    eq getYieldTimers(noYieldInformation) = empty .

    eq decreaseYieldTimers(([Oid: OID - (X,Y) | TR] , YIS), TIME) = [Oid: OID - (X,Y) | decrease(TR, TIME)] , decreaseYieldTimers(YIS, TIME) .
    eq decreaseYieldTimers(([Oid: OID | TR] , YIS), TIME) = [Oid: OID | decrease(TR, TIME)] , decreaseYieldTimers(YIS, TIME) .
    eq decreaseYieldTimers(noYieldInformation, TIME) = noYieldInformation .

    eq decreaseWorkTimer(< OID : Robot | workState : [OID2, (fixAt (X,Y)), TIME] >, TIME2) = < OID : Robot | workState : [OID2, (fixAt (X,Y)), (TIME monus TIME2)] > .
    eq decreaseWorkTimer(< OID : Robot | workState : [OID2, resupplyTask, TIME] >, TIME2) = < OID : Robot | workState : [OID2, resupplyTask, (TIME monus TIME2)] > .
    eq decreaseWorkTimer(< OID : Robot | workState : [OID2, unloadTask, TIME] >, TIME2) = < OID : Robot | workState : [OID2, unloadTask, (TIME monus TIME2)] > .
    eq decreaseWorkTimer(< OID : Robot | >, TIME) = < OID : Robot | > [owise] .

    eq decreaseTimers(< OID : Robot | auctions : AUS, yieldStatus : YIS, waitingStatus : WI >, TIME) = 
        decreaseWorkTimer(< OID : Robot | auctions : decreaseAuctionTimers(AUS, TIME), yieldStatus : decreaseYieldTimers(YIS, TIME), waitingStatus : decreaseWaitingTimers(WI, TIME) >, TIME) .

    eq mustRejectTask(< OID : Robot | state : INC >) = true .
    eq mustRejectTask(< OID : Robot | curTask : TID >) = true .
    eq mustRejectTask(< OID : Robot | >) = false [owise] .

    eq needsCPoint(< OID : Worker | >, TID) = false .
    eq needsCPoint(< OID : Transporter | cargoLevel : CL >, TID) = CL == full .

    ceq removeYieldInformation(OID, (YI ,YIS)) = YIS if getOid(YI) == OID .
    eq removeYieldInformation(OID, YIS) = YIS [owise] .

    ceq needsResupply(< OID : Worker | curCargo : CARGO >, [OID2, wateringTask]) = true if CARGO =/= water .
    ceq needsResupply(< OID : Worker | curCargo : CARGO >, [OID2, fertilizeTask]) = true if CARGO =/= fertilizer .
    ceq needsResupply(< OID : Worker | curCargo : CARGO >, [OID2, plantTask]) = true if CARGO =/= seed .
    eq needsResupply(< OID : Worker | cargoLevel : emptyCargo >, TID) = true .
    eq needsResupply(< OID : Robot | >, TID) = false [owise] .

    eq cPointLocCheck(< OID : Robot | >, TID, (X,Y)) = if needsCPoint(< OID : Robot | >, TID) then (X,Y) else nil fi .

    eq resupplyLocCheck(< OID : Robot | >, TID, (X,Y)) = if needsResupply(< OID : Robot | >, TID) then (X,Y) else nil fi .

    eq performChecks(< OID : Robot | >, TID, < OID2 : ResupplyStation | coordinate : (X,Y) > < OID3 : CollectionStation | coordinate : (X2,Y2) > CONF) =
        resupplyLocCheck(< OID : Robot | >, TID, (X,Y)) cPointLocCheck(< OID : Robot | >, TID, (X2,Y2)) .

    ceq canComplete(< OID : Worker | state : FUNC, curTask : TS >, [OID2, fixAt (X,Y)]) = true if (TS == noTask) or (TS == [OID2, fixAt (X,Y)]) .
    eq canComplete(< OID : Worker | state : FUNC, cargoLevel : full, curCargo : water >, [OID2, wateringTask]) = true .
    eq canComplete(< OID : Worker | state : FUNC, cargoLevel : full, curCargo : fertilizer >, [OID2, fertilizeTask]) = true .
    eq canComplete(< OID : Worker | state : FUNC, cargoLevel : full, curCargo : seed >, [OID2, plantTask]) = true .
    eq canComplete(< OID : Transporter | state : FUNC, cargoLevel : emptyCargo >, [OID2, harvestTask]) = true .
    eq canComplete(< OID : Robot | state : INC >, TID) = false .
    ceq canComplete(< OID : Robot | curTask : TID >, TID2) = false if TID =/= noTask .
    eq canComplete(< OID : Robot | >, TID) = false [owise] .

    eq calculatePathLength(< OID : Robot | >, TID, < OID2 : Map | >) = listLength(planPath(< OID : Robot | >, TID, < OID2 : Map | >)) .

    ***Returns the locations the robot must visit on its path to complete the task.
    eq $planPathLocations(< OID : Robot | coordinate : (X,Y) >, [OID2, TT], < OID2 : Locatable | coordinate : (X2,Y2) > CONF) =
        (X,Y) performChecks(< OID : Robot | >, [OID2, TT], < OID2 : Locatable | coordinate : (X2,Y2) > CONF) (X2,Y2) .

    ***LCO2 is obstructions
    eq $planPath(< OID : Map | >, LCO, LCO2) = dijkstraChain(getIntersections(modifyMap(< OID : Map | >, LCO, LCO2)), LCO) .

    eq planPath(< OID : Robot | coordinate : (X,Y), obstructionPoints : LCO >, [OID2, fixAt (X2,Y2)], < OID3 : Map | intersections : CONF >) =
        shortenEndBy($planPath(< OID3 : Map | >, (X,Y) (X2,Y2), LCO), ROBOT-SIZE) .
        
    eq planPath(< OID : Robot | coordinate : (X,Y), obstructionPoints : LCO >, [OID2, TT], < OID3 : Map | intersections : CONF >) =
        rmvNeighborDuplicates(
            $planPath(
                < OID3 : Map | >,
                $planPathLocations(< OID : Robot | >, [OID2, TT], CONF),
                LCO
            )
        ) [owise] .

    ***can directly dijkstra with (X2,Y2) added as destination
    eq planPath(< OID : Robot | coordinate : (X,Y), obstructionPoints : LCO >, (X2,Y2), < OID3 : Map | intersections : CONF >) =
        rmvNeighborDuplicates(
            $planPath(< OID3 : Map | >, (X,Y) (X2,Y2), LCO)
        ) .


    ceq modifyPath(LCO (X,Y) (X2,Y2) LCO2, (X3,Y3) LCO3) = modifyPath(LCO (X,Y) (X3,Y3) (X2,Y2) LCO2, LCO3)
        if ((X3,Y3) =/= (X,Y) and (X3,Y3) =/= (X2,Y2) and findPathPair((X3,Y3), (X,Y) (X2,Y2)) =/= nil) . 
    eq modifyPath(LCO, LCO2) = LCO [owise] .
    
    ***Todo this can be generalized with the road thingy
    ceq $breaksPath((X,Y) (X,Y2), {OID2 : CID | (X,Y3) , north , IS}) = true if Y > Y2 .
    ceq $breaksPath((X,Y) (X2,Y), {OID2 : CID | (X3,Y) , east , IS}) = true if X > X2 .
    ceq $breaksPath((X,Y) (X,Y2), {OID2 : CID | (X,Y3) , south , IS}) = true if Y < Y2 .
    ceq $breaksPath((X,Y) (X2,Y), {OID2 : CID | (X3,Y) , west , IS}) = true if X < X2 .
    eq $breaksPath(LCO, REPS) = false [owise] .
    
    ceq breaksPath(LCO, {OID2 : CID | (X,Y) , IS}) = false if findPathPair((X,Y), LCO) == nil .
    eq breaksPath(LCO, {OID2 : Obstacle | (X,Y) , IS}) =
        if findPathPair((X,Y), LCO) == nil then
            false
        else
            true
        fi .
    eq breaksPath(LCO, {OID2 : CID | (X,Y) , D , IS}) = $breaksPath(findPathPair((X,Y), LCO), {OID2 : CID | (X,Y) , D , IS}) .

    eq $yieldLocationHelper((X,Y), ({(X,Y) (X,Y2)} , RS)) =
        if Y < Y2
        then
            (X, Y + YIELD-GAP)
        else
            (X, Y - YIELD-GAP)
        fi .
    eq $yieldLocationHelper((X,Y), ({(X,Y) (X2,Y)} , RS)) =
        if X < X2
        then
            (X + YIELD-GAP, Y)
        else
            (X - YIELD-GAP, Y)
        fi .

    eq yieldLocation(< OID : PassIntersection | coordinate : (X,Y) >, RS, LCO (X,Y) LCO2) =
        if findRoad((X,Y), RS) \ findRoad(last(LCO) first(LCO2), RS) =/= empty  then
            $yieldLocationHelper((X,Y), findRoad((X,Y), RS) \ findRoad(last(LCO) first(LCO2), RS))
        else
            nil
        fi .
    eq yieldLocation(< OID : Intersection | >, RS, LCO) = nil [owise] .
        
    eq yieldIntersection([< OID : PassIntersection | coordinate : (X,Y) >, 0, "Start"] PQ, < OID2 : Map | roads : RS >, LCO, (X,Y)) = 
        if yieldLocation(< OID : PassIntersection | >, RS, LCO) =/= nil
        then
            yieldLocation(< OID : PassIntersection | >, RS, LCO)
        else
            if | findRoad((X,Y), RS) | == 1
            then
                nil
            else
                yieldIntersection(PQ, < OID2 : Map | >, LCO, (X,Y))
            fi
        fi .
    ceq yieldIntersection([< OID : PassIntersection | coordinate : (X,Y) >, RI, OID2] PQ, < OID3 : Map | intersections : CONF, roads : RS >, LCO, (X2,Y2)) =
        if RI == ratInf
        then
            nil
        else
            if yieldLocation(< OID : PassIntersection | >, RS, LCO) =/= nil
            then
                if not pathConflict(dijkstraSearch(CONF, (X2,Y2), (X,Y)), LCO)
                then
                    dijkstraSearch(CONF, (X2,Y2), (X,Y)) yieldLocation(< OID : PassIntersection | >, RS, LCO)
                else
                    yieldIntersection(PQ, < OID3 : Map | >, LCO, (X2,Y2))
                fi
            else
                yieldIntersection(PQ, < OID3 : Map | >, LCO, (X2,Y2))
            fi
        fi
        if OID2 =/= "Start" .
    eq yieldIntersection(noQueue, < OID : Map | >, LCO, (X,Y)) = nil .

    eq yieldPath(< OID : Robot | coordinate : (X,Y), obstructionPoints : LCO >, < OID2 : Map | >, LCO2) =
        yieldIntersection(
            updateDistances(PQConvert(getIntersections(modifyMap(< OID2 : Map | >, (X,Y) LCO2, LCO)), (X,Y))),
            modifyMap(< OID2 : Map | >, (X,Y) LCO2, LCO),
            modifyPath(LCO2, (X,Y)),
            (X,Y)
        ) .

    ***valid yieldIntersection has at least 3 connected roads.
    eq conflictYieldIntersection([< OID : PassIntersection | coordinate : (X,Y) >, RI, OID2] PQ, < OID3 : Map | roads : RS >) =
        if RI == ratInf
        then
            none
        else
            if | findRoad((X,Y), RS) | > 2
            then
                < OID : PassIntersection | >
            else
                conflictYieldIntersection(PQ, < OID3 : Map | >)
            fi
        fi .
        
    eq $conflictYieldPath(< OID : PassIntersection | coordinate : (X,Y) >, < OID2 : Map | intersections : CONF, roads : RS >, (X2,Y2)) =
        dijkstraSearch(CONF, (X2,Y2), (X,Y)) yieldLocation(< OID : PassIntersection | >, RS, dijkstraSearch(CONF, (X2,Y2), (X,Y))) .

    eq $conflictYieldPath(none, < OID2 : Map | >, (X2,Y2)) = nil .

    eq conflictYieldPath(< OID : Robot | coordinate : (X,Y), obstructionPoints : LCO >, < OID2 : Map | >) =
        $conflictYieldPath(
            conflictYieldIntersection(
                updateDistances(PQConvert(getIntersections(modifyMap(< OID2 : Map | >, (X,Y), LCO)), (X,Y))),
                modifyMap(< OID2 : Map | >, (X,Y), LCO)
            ),
            modifyMap(< OID2 : Map | >, (X,Y), LCO),
            (X,Y)
        ) .

    eq $yieldProtocol(< OID : Robot | remainingPath : LCO >, < OID2 : Map | >, OID3, nil) =
        < OID : Robot | remainingPath : conflictYieldPath(< OID : Robot | >, < OID2 : Map | >) >
        dly(msg conflictYield(conflictYieldPath(< OID : Robot | >, < OID2 : Map | >)) from OID to OID3, MSG-DELAY) .

    eq $yieldProtocol(< OID : Robot | negotiations : NS, yieldStatus : YIS >, < OID2 : Map | >, OID3, LCO (X,Y) (X2,Y2)) =
        < OID : Robot | remainingPath : LCO (X,Y) (X2,Y2), negotiations : endNegotiation(NS, OID3), yieldStatus : addYieldInformation(OID3, YIS) >
        dly(msg yielding (X,Y) from OID to OID3, MSG-DELAY) .

    eq $yieldProtocol(< OID : Robot | coordinate : (X,Y), negotiations : NS, yieldStatus : YIS >, < OID2 : Map | >, OID3, (X2,Y2)) =
        < OID : Robot | remainingPath : (X2,Y2), negotiations : endNegotiation(NS, OID3), yieldStatus : addYieldInformation(OID3, YIS) >
        dly(msg yielding (X,Y) from OID to OID3, MSG-DELAY) .

    eq $conflictYieldProtocol(< OID : Robot | state : STATE, negotiations : NS >, < OID2 : Map | >, OID3, nil) =
        < OID : Robot | state : deadlocked, negotiations : endNegotiation(NS, OID3) >
        dly(msg noYield from OID to OID3, MSG-DELAY) .
    
    eq $conflictYieldProtocol(< OID : Robot | negotiations : NS, yieldStatus : YIS >, < OID2 : Map | >, OID3, LCO (X,Y) (X2,Y2)) =
        < OID : Robot | remainingPath : LCO (X,Y) (X2,Y2), negotiations : endNegotiation(NS, OID3), yieldStatus : addYieldInformation(OID3, YIS) >
        dly(msg yielding (X,Y) from OID to OID3, MSG-DELAY) .

    eq conflictYieldProtocol(< OID : Robot | coordinate : (X,Y) >, < OID2 : Map | >, OID3, LCO) =
        $conflictYieldProtocol(< OID : Robot | >, < OID2 : Map | >, OID3, yieldPath(< OID : Robot | >, < OID2 : Map | >, LCO)) .

    ***Robot moves out of way of LCO.
    eq yieldProtocol(< OID : Robot | coordinate : (X,Y), remainingPath : LCO, negotiations : NS, yieldStatus : YIS >, < OID2 : Map | >, OID3, LCO2) =
        if not pathConflict((LCO2), (X,Y))
        then
            < OID : Robot | remainingPath : nil, negotiations : endNegotiation(NS, OID3), yieldStatus : addYieldInformation(OID3, YIS) >
            dly(msg yielding sharedLocation((X,Y) LCO, LCO2) from OID to OID3, MSG-DELAY)
        else
            $yieldProtocol(< OID : Robot | >, < OID2 : Map | >, OID3, yieldPath(< OID : Robot | >, < OID2 : Map | >, LCO2))
        fi .

    eq addYieldInformation(OID, (X,Y), ([Oid: OID - (X2,Y2) | TR] , YIS)) =
        [Oid: OID - (X,Y) | YIELD-TIMER] , YIS .
    eq addYieldInformation(OID, (X,Y), YIS) =
        [Oid: OID - (X,Y) | YIELD-TIMER] , YIS [owise] .
    eq addYieldInformation(OID, ([Oid: OID | TR] ,YIS)) = 
        [Oid: OID | YIELD-TIMER] , YIS .
    eq addYieldInformation(OID, YIS) =
        [Oid: OID | YIELD-TIMER] , YIS [owise] .

    eq getOid([Oid: OID - (X,Y) | TR]) = OID .
    eq getOid([Oid: OID | TR]) = OID .

    *** An object is not detected if it is already registered or outside detection range.
    ***can detect, but ignore instead?
    ***TODO: Update the handling section instead, to ignore if worked on or if part of obspoints
    ***This would require updating the detect rules of robots. to ignore
***    eq < OID : Robot | curTask : [OID2, TT] > detects < OID2 : Locatable | > = false .
***    eq < OID : Robot | obstructionPoints : (X,Y) LCO > detects < OID2 : Locatable | coordinate : (X,Y) > = false .
    ceq < OID : Robot | coordinate : (X,Y), detected : REPS, detectionRange : DR > 
        detects 
        < OID2 : Locatable | coordinate : (X2,Y2) > 
        =
        true if (not OID2 in REPS) and (distance((X,Y), (X2,Y2)) <= DR) .
    eq < OID : Robot | > detects < OID2 : Locatable | > = false [owise] .

    ceq detect(
            < OID : Robot | state : FUNC, detected : REPS >
            < OID2 : Obstacle | coordinate : (X2, Y2) > CONF
        ) 
        =
        detect(
            < OID : Robot | detected : ({OID2 : Obstacle | (X2, Y2)} , REPS) >
            < OID2 : Obstacle | > CONF
        )
        if < OID : Robot | > detects < OID2 : Obstacle | > .

    ceq detect(
            < OID : Robot | detected : REPS >
            < OID2 : Robot | state : STATE, coordinate : (X2, Y2), direction : D, speed : SP >
        CONF
        )
        =
        detect(
            < OID : Robot | detected : ({OID2 : Robot | (X2, Y2) , D , detectedSpeed(SP) , STATE} , REPS) >
            < OID2 : Robot | > CONF
        )
        if (< OID : Robot | > detects < OID2 : Robot | >) .
    eq detect(CONF) = CONF [owise] .

    crl [rescanTimerExpire] :
        < OID : Robot | waitingStatus : [BIS | TR | TR2] >
        CONF
        =>
        < OID : Robot | waitingStatus : [rescanWaitingOn(BIS, inDetectionRange(< OID : Robot | >, CONF)) | reset(TR) | TR2] >
        CONF
        if expired(TR) .
    
    crl [waitingTimerExpire] :
        < OID : Robot | waitingStatus : [BIS | TR | TR2] >
        =>
        < OID : Robot | waitingStatus : noWaitingInformation >
        if expired(TR2) .
    
    ***Another robot tells us to wait on it, add it to waitingInformation.
    crl [rcvPriorityWaitingMsg] :
        < OID : Robot | coordinate : (X,Y), remainingPath : LCO, waitingStatus : WI, negotiations : NS >
        msg priorityWaiting & path((X2,Y2) LCO2) from OID2 to OID
        =>
        < OID : Robot | waitingStatus : addWaiting(WI, OID2, (X2,Y2)), negotiations : endNegotiation(NS, OID2) >
        if pathConflict((X,Y) LCO, (X2,Y2)) .

    crl [yieldTimerExpire] :
        < OID : Robot | yieldStatus : YI , YIS >
        =>
        < OID : Robot | yieldStatus : YIS >
        if yieldExpired(YI) .

    rl [finishWaiting] : 
        < OID : Robot | state : waiting, waitingStatus : noWaitingInformation, negotiations : NS >
        =>
        if NS == none
        then
            < OID : Robot | state : idle >
        else
            < OID : Robot | state : negotiating >
        fi .
        
    rl [rcvWonMsg] :
        < OID : Robot | >
        msg won TID from OID2 to OID
        =>
        if mustRejectTask(< OID : Robot | >)
        then
            < OID : Robot | >
            dly(msg reject TID from OID to OID2, MSG-DELAY)
        else
            < OID : Robot | state : planningPath, speed : 0, curTask : TID >
            dly(msg accept TID from OID to OID2, MSG-DELAY)
        fi .
    
    ***A robot that has a task and is not involved in resolving a conflict will path towards task
    ***TODO: Prevent it from this being applicable if it is where it needs to be.
    crl [restartTask] :
        < OID : Robot | state : idle, remainingPath : nil, yieldStatus : noYieldInformation, curTask : TID >
        CONF
        =>
        < OID : Robot | state : planningPath >
        CONF 
        if not arrivedTask(< OID : Robot | >, CONF) .

    ***If a robot receives a higher cost message it will reroute.
    crl [rcvHigherCostMsg] :
        < OID : Robot | state : FUNC, obstructionPoints : LCO, negotiations : NS [OID2 | (X,Y) | cost(RI)], curTask : TID >
        < OID3 : Map | >
        msg path((X2,Y2) LCO2) & pathCost(cost(RI2)) from OID2 to OID
        =>
        < OID : Robot | state : planningPath, obstructionPoints : (X2,Y2) LCO, negotiations : NS >
        < OID3 : Map | >
        dly(msg noConflict from OID to OID2, MSG-DELAY)
        if RI2 > RI .

    rl [rcvEqualCostMsg] :
        < OID : Robot | state : FUNC, obstructionPoints : LCO, negotiations : NS [OID2 | (X,Y) | cost(RI)], curTask : TID >
        < OID3 : Map | > 
        msg path((X2,Y2) LCO2) & pathCost(cost(RI)) from OID2 to OID
        =>
        ***If both costs are ratInf, the conflict is not solvable with rerouting.
        ***Start the yielding process to check if the conflict can be solved with one of the robots yielding.
        if RI == ratInf
        then
            if OID < OID2
            then
                ***attempt to yield to the path
                ***If we find one, then we can yield and send yielding back
                ***If we cant, do conflict yield, and if there is no yield?
                ***Since we know both receive the cost, we know other robot is doing same calculation, so we can send noyield.
                yieldProtocol(
                    < OID : Robot | state : negotiating, speed : 0, negotiations : addNegotiation(NS, [OID2 | (X2,Y2) | cost(RI)]) >,
                    < OID3 : Map | >,
                    OID2,
                    (X2,Y2) LCO2
                )
                < OID3 : Map | >
            else
                < OID : Robot | >
                < OID3 : Map | >
            fi
        else
            if OID < OID2
            then
                < OID : Robot | state : planningPath, obstructionPoints : (X2,Y2) LCO, negotiations : NS >
                < OID3 : Map | > 
                dly(msg noConflict from OID to OID2, MSG-DELAY)
            else
                < OID : Robot | >
                < OID3 : Map | > 
            fi 
        fi .


    ***If a robot receives a lower cost message, it knows that the other should reroute
    ***It therefore does not change beavior
    crl [rcvLowerCostMsg] :
        < OID : Robot | negotiations : NS [OID2 | (X,Y) | cost(RI)] >
        msg path(LCO) & pathCost(cost(RI2)) from OID2 to OID
        =>
        < OID : Robot | >
        if RI < RI2  .

    crl [rcvInvalidCost] :
        < OID : Robot | negotiations : NS >
        msg path(LCO) & pathCost(COST) from OID2 to OID
        =>
        < OID : Robot | >
        if not inNegotiationSet(NS, OID2) .
        
    rl [rcvNoConflictMsg] :
        < OID : Robot | negotiations : NS >
        msg noConflict from OID2 to OID
        =>
        < OID : Robot | negotiations : endNegotiation(NS, OID2) > .

    rl [rcvYieldingMsg] :
        < OID : Robot | negotiations : NS, yieldStatus : YIS >
        msg yielding (X,Y) from OID2 to OID
        =>
        < OID : Robot | negotiations : endNegotiation(NS, OID2), yieldStatus : addYieldInformation(OID2, (X,Y), YIS) > .

    crl [rcvConflictYieldHigherPriority] :
        < OID : Robot | coordinate : (X,Y), remainingPath : LCO, state : negotiating, negotiations : NS >
        < OID2 : Map | >
        msg conflictYield(LCO2) from OID3 to OID
        =>
        < OID : Robot | negotiations : endNegotiation(NS, OID3) >
        < OID2 : Map | >
        dly(msg priorityWaiting & path((X,Y) LCO) from OID to OID3, MSG-DELAY)
        if priorityState(< OID : Robot | >) == priorityWaiting
            or priorityState(< OID : Robot | negotiations : endNegotiation(NS, OID3) >) == priorityNegotiating .
    
    crl [rcvConflictYield] :
        < OID : Robot | state : negotiating, negotiations : NS >
        < OID2 : Map | >
        msg conflictYield(LCO2) from OID3 to OID
        =>
        conflictYieldProtocol(< OID : Robot | >, < OID2 : Map | >, OID3, LCO2)
        < OID2 : Map | >
        if priorityState(< OID : Robot | >) =/= priorityWaiting and
            priorityState(< OID : Robot | negotiations : endNegotiation(NS, OID3) >) =/= priorityNegotiating .

    rl [rcvNoYieldMsg] :
        < OID : Robot | negotiations : NS >
        msg noYield from OID2 to OID
        =>
        < OID : Robot | state : deadlocked, negotiations : endNegotiation(NS, OID2) > .

    ***when planning path, if it is not possible, then auction away tasks.
    rl [planPath] :
        < OID : Robot | state : planningPath, detected : empty, curTask : TID >
        < OID3 : Map | >
        CONF
        =>
        if planPath(< OID : Robot | >, TID, < OID3 : Map | >) == nil
        then
            initiateAuction(< OID : Robot | state : idle, remainingPath : nil > , TID, CONF)
            < OID3 : Map | >
            CONF
        else
            < OID : Robot | state : idle, remainingPath : planPath(< OID : Robot | >, TID, < OID3 : Map | >) >
            < OID3 : Map | >
            CONF
        fi .

    ***Notify another robot when Self has reached the yieldIntersection, Other can move again.
    rl [finishYieldingTask] :
        < OID : Robot | state : FUNC, coordinate : (X,Y), yieldStatus : ([Oid: OID2 - (X,Y) | TR], YIS) >
        < OID2 : Robot | >
        < OID3 : Map | >
        =>
        < OID : Robot | state : FUNC, yieldStatus : YIS >
        < OID2 : Robot | >
        < OID3 : Map | >
        dly(msg completedYield from OID to OID2, MSG-DELAY) .

    crl [rcvPathNegotiationNoConflict] :
        < OID : Robot | coordinate : (X,Y), state : STATE, remainingPath : LCO >
        msg requestPath & path(LCO2) & PS from OID2 to OID
        =>
        < OID : Robot | >
        dly(msg noConflict from OID to OID2, MSG-DELAY)
        if ((not pathConflict(LCO2, (X,Y) LCO)) and STATE == moving) or  ((not pathConflict(LCO2, (X,Y))) and STATE =/= moving) .

    crl [rcvPathNegotiationIdle] :
        < OID : Robot | coordinate : (X,Y), negotiations : NS >
        < OID2 : Map | >
        msg requestPath & path((X2,Y2) LCO) & PS from OID3 to OID
        =>
        yieldProtocol(
            < OID : Robot | state : negotiating, speed : 0, negotiations : addNegotiation(NS, [OID3 | (X2,Y2) | noCost]) >,
            < OID2 : Map | >,
            OID3,
            (X2,Y2) LCO
        )
        < OID2 : Map | >
        if pathConflict((X2,Y2) LCO, (X,Y)) and priorityState(< OID : Robot | >) == priorityIdle .

    crl [rcvPathNegotiationHighestPriority] :
        < OID : Robot | coordinate : (X,Y), remainingPath : LCO, negotiations : NS >
        msg requestPath & path(LCO2) & PS from OID2 to OID
        =>
        < OID : Robot | >
        dly(msg path((X,Y) LCO) & priorityWaiting from OID to OID2, MSG-DELAY)
        if pathConflict(LCO2, (X,Y))
            and (priorityState(< OID : Robot | >) == priorityWorking
            or priorityState(< OID : Robot | >) == priorityWaiting
            or priorityState(< OID : Robot | negotiations : endNegotiation(NS, OID2) >) == priorityNegotiating) .

    crl [rcvPathNegotiationTask] :
        < OID : Robot | coordinate : (X,Y), remainingPath : LCO, negotiations : NS >
        < OID2 : Map | >
        msg requestPath & path((X2,Y2) LCO2) & PS from OID3 to OID
        =>
        if PS == priorityTask
        then
            < OID : Robot | state : negotiating, speed : 0, negotiations : addNegotiation(NS, [OID3 | (X2,Y2) | replanCost(< OID : Robot | >, < OID2 : Map | >, (X2, Y2))]) >
            < OID2 : Map | >
            dly(msg path((X,Y) LCO) & pathCost(replanCost(< OID : Robot | >, < OID2 : Map | >, (X2, Y2))) from OID to OID3, MSG-DELAY)
        else
            yieldProtocol(
                < OID : Robot | state : negotiating, speed : 0, negotiations : addNegotiation(NS, [OID3 | (X2,Y2) | noCost]) >,
                < OID2 : Map | >,
                OID3,
                (X2,Y2) LCO2
            )
            < OID2 : Map | >
        fi
        if pathConflict((X2,Y2) LCO2, (X,Y) LCO) and priorityState(< OID : Robot | negotiations : endNegotiation(NS, OID3) >) == priorityTask .

    ***TODO: Fix yielding towards same intersection.
    crl [rcvPathNegotiationYielding] : 
        < OID : Robot | coordinate : (X,Y), remainingPath : LCO, negotiations : NS >
        < OID2 : Map | >
        msg requestPath & path((X2,Y2) LCO2) & PS from OID3 to OID
        =>
        if PS == priorityYielding
        then
            ***Tiebreaker, lowest oid maintains, other yields.
            if OID < OID3
            then
                if LCO == nil
                then
                    < OID : Robot | >
                    < OID2 : Map | >
                    dly(msg priorityWaiting & path((X,Y)) from OID to OID3, MSG-DELAY)
                else
                    < OID : Robot | >
                    < OID2 : Map | >
                fi
            else
                yieldProtocol(
                    < OID : Robot | state : negotiating, speed : 0, negotiations : addNegotiation(NS, [OID3 | (X2,Y2) | noCost]) >,
                    < OID2 : Map | >,
                    OID3,
                    (X2,Y2) LCO2
                )
                < OID2 : Map | >
            fi
        else
            if pathConflict((X,Y) LCO, (X2,Y2) LCO2)
            then
                < OID : Robot | >
                < OID2 : Map | >
                dly(msg requestPath & path(LCO) & priorityYielding from OID to OID3, MSG-DELAY)
            else
                < OID : Robot | >
                < OID2 : Map | >
                dly(msg path((X,Y) LCO) & priorityWaiting from OID to OID3, MSG-DELAY)
            fi
        fi
        if priorityState(< OID : Robot | negotiations : endNegotiation(NS, OID3) >) == priorityYielding
        and pathConflict((X2,Y2) LCO2, (X,Y) LCO) .

    rl [rcvCompletedYieldingMsg] :
        < OID : Robot | state : FUNC, yieldStatus : YIS >
        msg completedYield from OID2 to OID
        =>
        < OID : Robot  | yieldStatus : removeYieldInformation(OID2, YIS) > .

    ***Workers discard transporter tasks
    rl [rcvWrongWorkerTask] :
        < OID : Worker | >
        msg [OID2, TPT] from OID3 to OID
        =>
        < OID : Worker | > .

    ***Transporters discard worker tasks.
    rl [rcvWrongTransporterTask] :
        < OID : Transporter | >
        msg [OID2, WT] from OID3 to OID
        =>
        < OID : Transporter | > .
        
    ***When robot is finished with all negotiations, check if it needs to wait for someone else.
    rl [finishNegotiating] :
        < OID : Robot | state : negotiating, negotiations : none, waitingStatus : WI >
        =>
        if WI == noWaitingInformation
        then
            < OID : Robot | state : idle >
        else
            < OID : Robot | state : waiting >
        fi .

    crl [rcvTaskMsgBusy] :
        < OID : Robot | >
        msg TID from OID2 to OID
        =>
        < OID : Robot | >
        if mustRejectTask(< OID : Robot  | >) .

    ***can only accept tasks of correct type
    crl [rcvTaskMsgFree] :
        < OID : Robot | coordinate : (X,Y), obstructionPoints : LCO >
        < OID2 : Map | intersections : CONF >
        msg [OID3, TT] from OID3 to OID
        =>
        < OID : Robot | >
        < OID2 : Map | >
        dly(msg bid cost(calculatePathLength(< OID : Robot | >, [OID3, TT], < OID2 : Map | >)) on [OID3, TT] from OID to OID3, MSG-DELAY)
        if not mustRejectTask(< OID : Robot | >) and compatibleTaskType(< OID : Robot | >, TT) .

    ***Handling of detected object
    ***Prevent rehandling of already deteected failed robots or obstacles.
    rl [detectIgnoreFix] :
        < OID : Robot | state : FUNC, detected : ({OID2 : CID | (X,Y) , IS} , REPS), workState : [OID2, fixAt (X,Y), TIME] >
        =>
        < OID : Robot | state : FUNC, detected : REPS > .
    
    rl [detectIgnoreFix2] :
        < OID : Robot | state : FUNC, detected : ({OID2 : CID | (X,Y) , IS} , REPS), obstructionPoints : (X,Y) LCO >
        =>
        < OID : Robot | state : FUNC, detected : REPS > .

    rl [detectFunctioningRobot] :
        < OID : Robot | detected : ({OID2 : Robot | FUNC , IS} , REPS) >
        < OID3 : Map | >
        =>
        safetyChecks(< OID : Robot | detected : REPS >, {OID2 : Robot | FUNC , IS}, < OID3 : Map | >)
        < OID3 : Map | > .

    crl [detectFixCapable] :
        < OID : Robot | state : FUNC, 
            coordinate : (X,Y), 
            detected : (REP , REPS), 
            obstructionPoints : LCO,
            workState : WS
        >
        =>
        < OID : Robot | state : planningPath,
            detected : REPS,
            curTask : [getRepOid(REP), fixAt getRepCoord(REP)],
            obstructionPoints : getRepCoord(REP) LCO
        >
        if canComplete(< OID : Robot | >, [getRepOid(REP), fixAt getRepCoord(REP)])
            and isFixable(REP)
            and WS =/= [getRepOid(REP), fixAt getRepCoord(REP)]
            and not getRepCoord(REP) in LCO . 
            
        --- else
        ---     if between((X2,Y2), (X,Y) LCO) =/= nil
        ---     then
        ---         initiateAuction(< OID : Robot | state : planningPath, detected : REPS, obstructionPoints : (X2,Y2) LCO2 >, [OID2, fixAt (X2,Y2)], CONF)
        ---         < OID2 : Robot | >
        ---         < OID3 : Map | >
        ---         CONF
        ---     else
        ---         initiateAuction(< OID : Robot | detected : REPS, obstructionPoints : (X2, Y2) LCO2 >, [OID2, fixAt (X2,Y2)], CONF)
        ---         < OID2 : Robot | >
        ---         < OID3 : Map | >
        ---         CONF
        ---     fi
        --- fi .

    ***prevent continuous auctioning of fix task. make it part of auctioneer
    --- crl [detectFixIncapable] : 
    ---     < OID : Robot | state : FUNC,
    ---         coordinate : (X,Y),
    ---         remainingPath : LCO,
    ---         detected : ({OID2 : Obstacle | (X2,Y2)} , REPS),
    ---         obstructionPoints : LCO2 >
    ---     < OID3 : Map | >
    ---     CONF
    ---     =>
    ---     if between((X2,Y2), (X,Y) LCO) =/= nil
    ---     then
    ---         initiateAuction(
    ---             < OID : Robot | state : planningPath,
    ---                 speed : 0,
    ---                 detected : REPS,
    ---                 obstructionPoints : (X2,Y2) LCO2
    ---             >,
    ---             [OID2, fixAt (X2,Y2)],
    ---             CONF
    ---         )
    ---         < OID3 : Map | >
    ---         CONF
    ---     else
    ---         initiateAuction(
    ---             < OID : Robot | detected : REPS,
    ---                 obstructionPoints : (X2, Y2) LCO2
    ---             >,
    ---             [OID2, fixAt (X2,Y2)],
    ---             CONF
    ---         )
    ---         < OID3 : Map | >
    ---         CONF
    ---     fi
    ---     if not canComplete(< OID : Robot | >, [OID2, fixAt (X2,Y2)]) . 

    crl [startObstacleRemoval] :
        < OID : Robot | coordinate : (X,Y), curTask : [OID2, fixAt (X2,Y2)], workState : noWork >
        < OID2 : Obstacle | coordinate : (X2,Y2) >
        =>
        < OID : Robot | state : removing, remainingPath : nil, speed : 0, workState : [OID2, (fixAt (X2,Y2)), REMOVAL-TIME] >
        < OID2 : Obstacle | > 
        if canComplete(< OID : Robot | >, [OID2, fixAt (X2,Y2)]) and distance((X,Y), (X2,Y2)) <= WORK-RANGE .

    rl [finishObstacleRemoval] :
        < OID : Robot | coordinate : (X,Y), curTask : [OID2, fixAt (X2,Y2)], workState : [OID2, (fixAt (X2,Y2)), 0] >
        < OID2 : Obstacle | coordinate : (X2,Y2) >
        CONF
        =>
        < OID : Robot | state : idle, curTask : noTask, workState : noWork >
        CONF .

    crl [startRobotRepair] :
        < OID : Robot | coordinate : (X,Y), curTask : [OID2, fixAt (X2,Y2)], workState : noWork >
        < OID2 : Robot | coordinate : (X2,Y2), state : INC > 
        =>
        < OID : Robot | state : repairing, workState : [OID2, (fixAt (X2,Y2)), REPAIR-TIME] >
        < OID2 : Robot | >
        if canComplete(< OID : Robot | >, [OID2, fixAt (X2,Y2)]) and distance((X,Y), (X2,Y2)) <= WORK-RANGE .

    rl [finishRobotRepair] :
        < OID : Robot | coordinate : (X,Y), curTask : [OID2, fixAt (X2,Y2)], workState : [OID2, (fixAt (X2,Y2)), 0] >
        < OID2 : Robot | state : INC, coordinate : (X2,Y2) >
        =>
        < OID : Robot | speed : 0, state : idle, curTask : noTask, workState : noWork > 
        < OID2 : Robot | state : idle > .

    crl [startResupply] :
        < OID : Robot | state : FUNC, coordinate : (X,Y), curTask : TID >
        < OID2 : ResupplyStation | coordinate : (X,Y) >
        =>
        < OID : Robot | speed : 0, state : resupplying, workState : [OID2, resupplyTask, RESUPPLY-TIME] > 
        < OID2 : ResupplyStation | >
        if needsResupply(< OID : Robot | >, TID) and FUNC =/= resupplying .

    rl [finishResupply] :
        < OID : Robot | coordinate : (X,Y), state : resupplying, curTask : [OID2, TT], workState : [OID3, resupplyTask, 0] >
        < OID4 : Map | intersections : < OID3 : ResupplyStation | coordinate : (X,Y) > CONF >
        =>
        finishedResupply(< OID : Robot | workState : noWork >, TT)
        < OID4 : Map | > .

    rl [startUnloading] : 
        < OID : Transporter | state : FUNC, coordinate : (X,Y), curCargo : produce, cargoLevel : full, workState : noWork >
        < OID2 : Map | intersections : < OID3 : CollectionStation | coordinate : (X,Y) > CONF >
        =>
        < OID : Transporter | speed : 0, state : unloading,  workState : [OID2, unloadTask, UNLOAD-TIME] >
        < OID2 : Map | intersections : < OID3 : CollectionStation | coordinate : (X,Y) > CONF > .
    
    rl [finishUnloading] :
        < OID : Transporter | state : unloading, curCargo : produce, coordinate : (X,Y), cargoLevel : full, workState : [OID2, unloadTask, 0] >
        < OID2 : Map | intersections : < OID3 : CollectionStation | coordinate : (X,Y), collections : N > CONF >
        =>
        < OID : Transporter | state : idle, cargoLevel : emptyCargo, workState : noWork >
        < OID2 : Map | intersections : < OID3 : CollectionStation | coordinate : (X,Y), collections : N + 1 > CONF > .

    rl [startGrowingLocTask] :
        < OID : Robot | state : FUNC, coordinate : (X,Y), curTask : [OID2, TT], remainingPath : nil, workState : noWork >
        < OID2 : Field | growingArea : A, coordinate : (X,Y), reserved : false >
        =>
        if canComplete(< OID : Robot | >, [OID2, TT])
        then
            < OID : Robot | remainingPath : (X,Y) coverArea((X,Y), A), workState : [OID2, TT], speed : 1, state : moving >
            < OID2 : Field | reserved : true >
        else
            < OID : Robot | speed : 0, state : planningPath >
            < OID2 : Field | >
        fi .

    rl [finishWorkerFieldTask] :
        < OID : Worker | state : FUNC, coordinate : (X,Y), remainingPath : nil, workState : [OID2, WT], cargoLevel : full >
        < OID2 : Field | coordinate : (X,Y), reserved : true >
        =>
        < OID : Worker | cargoLevel : emptyCargo, curTask : noTask, workState : noWork >
        taskComplete(< OID2 : Field | >, [OID2, WT]) . 

    rl [finishTransporterFieldTask] :
        < OID : Transporter | state : FUNC, coordinate : (X,Y), curTask : [OID2, harvestTask], remainingPath : nil, workState : [OID2, TPT], cargoLevel : emptyCargo >
        < OID2 : Field | coordinate : (X,Y), reserved : true >
        < OID3 : Map | intersections : < OID4 : CollectionStation | AS > CONF >
        =>
        < OID : Transporter | state : planningPath, curCargo : produce, cargoLevel : full, curTask : [OID4, unloadTask], workState : noWork >
        < OID3 : Map | >
        taskComplete(< OID2 : Field | >, [OID2, TPT]) .
endom