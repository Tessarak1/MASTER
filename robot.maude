***(Superclass for ht-robot and worker-robot classes. Contains the common operations
attributes and rules between the two types of robots. )***
load time-inf .
load pathfinder .
load message .
load map .
load growingLoc .
load collectionPoint .
load dock .
load human .
load obstacle .

omod ROBOT is 
    protecting MESSAGE .
    protecting OBSTACLE .
    protecting MAP .
    protecting HUMAN .
    protecting TIME-INF .
    protecting GROWINGLOC .
    protecting CPOINT .
    protecting DOCK .
    protecting PATHFINDER .

    sort CargoLevel .
    sorts WorkerCargo TransporterCargo Cargo .
    sort MovementType . 
    ***Type of object detected and Identificator.
    sort Representation .
    sort TaskStatus . 
    
    sort Information .
    subsorts Location Direction < Information .
    subsort Tid < TaskStatus .

    class Robot | detectionRange : Nat, cargoLevel : CargoLevel, curCargo : Cargo, detected : Representation, waitTimer : Rat, obstructionPoints : List-Location, curTask : TaskStatus .
    class Worker | movementType : MovementType .
    class Transporter .
    
    subclass Worker Transporter < Robot < Entity .

    op _;_ : Information Information -> Information [ctor assoc comm id: noInfo] .
    op noInfo : -> Information [ctor] .
    op TaskNr:_ : Nat -> Information [ctor] .
    op detectedSpeed : Rat -> Information [ctor] .
    op [W:_ F:_ H:_] : Level Level GrowingState -> Information [ctor] .

    op noTask : -> TaskStatus .

    ***Workers can plant fields or greenhouses, so they should have seeds as part of cargo.
    sort Seed .
    subsort Seed Water Fertilizer < WorkerCargo .
    subsort Produce < TransporterCargo .
    subsort TransporterCargo WorkerCargo < Cargo .
    subsort Empty Full < CargoLevel .

    ops flying ground : -> MovementType [ctor] .
    ops saladSeed potatoSeed turnipSeed pepperSeed tomatoSeed cucumberSeed wheatSeed ryeSeed peachSeed : -> Seed [ctor] .
    op distance : Object Object -> Rat [comm] .
    op processing : Representation Time -> Representation [frozen (1) right id: 0] .
    ***Robot does dijkstraSearch from the first intersection to the second.
    ***Used for shorthand to perform dijkstra on an updated map.
    op updatePath : Object Configuration Location Location -> Object .
    ***returns the front-facing location where the robots detection border is.
    op detectionBorder : Object -> Location .
    op updatePath : Object Configuration Location -> Object .
    op replanPath : Object Object Location -> Object .

    op nil : -> Representation [ctor] .
    op __ : Representation Representation -> Representation [assoc comm id: nil ctor] .
    ***Todo: refine this based on what information the robot should know.
    op {_:_|_} : Oid Cid Information -> Representation .
    ***Checks if an object already is in the representation .
    op _notIn_ : String Representation -> Bool .
    ***checks if the given represenation is incompatible with path. If continuing the path would lead to a head-on collision.
    ops breaksPath $breaksPath : List-Location Representation -> Bool .

    op canComplete : Object Information -> Bool .

    var CONF : Configuration .
    vars S S' S2 S2' S3 S4 : String .

    vars X Y : Rat .
    vars X2 Y2 : Rat .
    vars X3 Y3 : Rat .

    var N : Nat .
    var A : Area .
    vars LL LL2 : List-Location .
    var L L2 : Level .
    var GS : GrowingState .
    var C : Content .
    var TC : TransporterCargo .
    var STOR : Storage .
    var R : Representation .
    var PS : PathSet .
    var MC : MsgContent .
    var CID : Cid .
    var I : Information .
    vars D D2 : Direction .
    vars SP SP2 : Rat .
    var STATE : State .
    var REP : Representation .
    var TID : Tid .
    var TS : TaskStatus .

    eq canComplete(< S : Worker | cargoLevel : full, curCargo : water >, [W: low F: L2 H: GS]) = true .
    eq canComplete(< S : Worker | cargoLevel : full, curCargo : fertilizer >, [W: L F: low H: GS]) = true .
    eq canComplete(< S : Transporter | cargoLevel : empty >, [W: L F: L2 H: harvest]) = true .
    eq canComplete(< S : Robot | >, [W: L F: L2 H: GS]) = false [owise] .

    eq updatePath(< S : Robot | location : (X,Y) >, < S2 : Map | allIntersections : < S3 : Intersection | location : (X,Y) > < S4 : Intersection | location : (X2 ,Y2) > CONF >, (X2,Y2)) = 
        < S : Robot | remainingPath : (X,Y) dijkstraSearch(< S3 : Intersection | location : (X,Y) > < S4 : Intersection | location : (X2 ,Y2) > CONF, < S3 : Intersection | >, < S4 : Intersection | location : (X2,Y2) >)> .

    ***Robot, map and obstructionPoints    
    eq replanPath(< S : Robot | location : (X,Y), obstructionPoints : LL2 >, < S2 : Map | allIntersections : CONF, paths : PS >, (X2,Y2)) =
        updatePath(< S : Robot | state : moving >, transformMap(< S2 : Map | >, (X,Y), LL2), (X2,Y2)) .

    eq detectionBorder(< S : Robot | location : (X,Y), detectionRange : N, direction : north >) = (X, Y + N) .
    eq detectionBorder(< S : Robot | location : (X,Y), detectionRange : N, direction : east >) = (X + N, Y) .
    eq detectionBorder(< S : Robot | location : (X,Y), detectionRange : N, direction : south >) = (X, Y - N) .
    eq detectionBorder(< S : Robot | location : (X,Y), detectionRange : N, direction : north >) = (X - N, Y) .

    eq S notIn {S : CID | (X,Y) ; I} R = false .
    eq S notIn R = true [owise] .
 
    ceq $breaksPath((X,Y) (X,Y2), {S2 : CID | (X,Y3) ; north ; I}) = true if Y > Y2 .
    ceq $breaksPath((X,Y) (X2,Y), {S2 : CID | (X3,Y) ; east ; I}) = true if X > X2 .
    ceq $breaksPath((X,Y) (X,Y2), {S2 : CID | (X,Y3) ; south ; I}) = true if Y < Y2 .
    ceq $breaksPath((X,Y) (X2,Y), {S2 : CID | (X3,Y) ; west ; I}) = true if X < X2 .
    eq $breaksPath(LL, R) = false [owise] .
    ceq breaksPath(LL, {S2 : CID | (X,Y) ; I}) = false if between((X,Y), LL) == nil .
    eq breaksPath(LL, {S2 : Obstacle | (X,Y) ; I}) =
        if between((X,Y), LL) == nil then
            false
        else
            true
        fi .
    eq breaksPath(LL, {S2 : CID | (X,Y) ; D ; I}) = $breaksPath(between((X,Y), LL), {S2 : CID | (X,Y) ; D ; I}) [owise] .

    ***Rules for path replanning
    rl [replanPathNoTask] :
        < S : Robot | state : replanning, remainingPath : LL (X,Y), detected : nil, curTask : noTask >
        < S2 : Map | >
        =>
        replanPath(< S : Robot | >, < S2 : Map | >, (X,Y))
        < S2 : Map | > .

    rl [replanPathTask] :
        < S : Robot | state : replanning, detected : nil, curTask : [S3 , N] >
        < S2 : Map | allIntersections : < S3 : Locatable | location : (X,Y) > CONF >
        =>
        replanPath(< S : Robot | >, < S2 : Map | >, (X,Y))
        < S2 : Map | > .

    ***rules for parsing through detections
    crl [dismissDetection] :
        < S : Robot | state : STATE, detected : R >
        =>
        < S : Robot | detected : nil >
        if ((STATE == idle) or (STATE == waiting)) and (R =/= nil) .

    rl [detectGrowingLocFree] :
        < S : Robot | detected : {S2 : GrowingLoc | (X2,Y2) ; [W: L F: L2 H: GS] ; TaskNr: N} REP, curTask : noTask >
        =>
        if L == good and L2 == good and GS =/= harvest then
            < S : Robot | detected : REP >
        else
            if canComplete(< S : Robot | >, [W: L F: L2 H: GS]) then
                < S : Robot | state : replanning, detected : REP, curTask : [S2 , N] >
            else
                ***Start auction
                < S : Robot | >
            fi
        fi .

    rl [detectGrowingLocBusy] :
        < S : Robot | detected : {S2 : GrowingLoc | I}, curTask : TID >
        =>
        < S : Robot | > .

    ***When approaching a slower entity, brake to match speed.
    crl [approachSameDirectionEntity] : 
        < S : Robot | location : (X,Y), speed : SP, direction : D, detected : {S2 : HumanVehicle | (X2,Y2) ; D ; detectedSpeed(SP2) ; I} R >
        =>
        if < S : Robot | > directlyApproaching (X2,Y2) then
            if (distance((X,Y), (X2,Y2)) / SP) < 5 then
                < S : Robot | speed : 0, state : idle, detected : R > 
            else
                if SP2 < SP then
                    < S : Robot | speed : SP2, detected : R >
                else
                    < S : Robot | detected : R >
                fi
            fi
        else
            < S : Robot | detected : R >
        fi
        if SP > 0 .

    ***if the detected obstacle is in the way then the robot will have to replan its path.
    ***If the robot no longer has a path to its goal, it will cancel the current task and work on removing the obstacle.
    crl [detectObstacle] : 
        < S : Robot | location : (X,Y), remainingPath : LL (X2,Y2), state : STATE, detected : {S2 : Obstacle | (X3,Y3) ; I} R, obstructionPoints : LL2 > 
        < S3 : Map | >
        =>
        ***if the obstacle is in the robot's way, add it to obstructionPoints and prepare to replan path.
        if between((X3,Y3), (X,Y) LL (X2,Y2)) =/= nil then
            < S : Robot | state : replanning, detected : R, obstructionPoints : (X3,Y3) LL2 >
            < S3 : Map | > 
            (msg obstacleAt (X3,Y3) from S to worker)
        else
            < S : Robot | detected : R >
            < S3 : Map | > 
            (msg obstacleAt (X3,Y3) from S to worker)
        fi
        if (STATE == moving) or (STATE == replanning) .

    ***Robot - Human interaction rules.
    crl [detectHumanPerson] :
        < S : Robot | location : (X,Y), speed : SP, direction : D, state : STATE, detected : {S2 : Human | (X2,Y2) ; detectedSpeed(SP2) ; I} >
        =>
        if < S : Robot | > directlyApproaching (X2,Y2) then
            ***if too close then start braking
            if (distance((X,Y), (X2,Y2)) / SP) < 3 then
                < S : Robot | speed : 0 >
            else
                < S : Robot | speed : 5 >
            fi
        else
            ***If the human is close but not in front of the robot slow down to limit possible accidents.
            < S : Robot | speed : 5 >
        fi
        if (STATE == moving) or (STATE == replanning) .

    ***In contrast to detection of person, if a human operated vehicle is on the planned path, replan path.
    crl [detectHumanVehicle] : 
        < S : Robot | location : (X,Y), remainingPath : LL, speed : SP, state : STATE, detected : {S2 : HumanVehicle | (X2,Y2) ; D ; I} R, obstructionPoints : LL2 >
        < S3 : Map | >
        =>
        if breaksPath((X,Y) LL, {S2 : HumanVehicle | (X2,Y2) ; D ; I}) then
            < S : Robot | state : replanning, detected : R, obstructionPoints : LL2 >
            < S3 : Map | >
        else
            < S : Robot | detected : R >
            < S3 : Map | >
        fi
        if (STATE == moving) or (STATE == replanning) .

    ***When a robot detects another on its path on an immediate collision course, if they are too close then it will immediately try to stop to limit the potential damage.
    ***If they are further away, then start communication to check which one will replan path.
    crl [detectIncomingRobot] :
        < S : Robot | location : (X,Y), speed : SP, direction : D, state : moving, detected : {S' : Robot | (X2,Y2) ; D2 ; I} R >
        =>
        ***Collision prevention with braking
        if (distance((X,Y), (X2,Y2)) / SP) > 3 then
            < S : Robot | speed : 0, state : waiting, detected : R, waitTimer : 10 >
        else
            < S : Robot | speed : 5, detected : R >
        fi
        if opposite(D,D2) .

    ***When first detecting a robot request its path and transmit own path to check for collision.
    rl [detectRobot] :
        < S : Robot | location : (X,Y), remainingPath : LL, detected : {S2 : Robot | (X2,Y2)} R >
        =>
        (msg requestPath path((X,Y) LL) from S to S2)
        < S : Robot | > .

    rl [detectHuman] :
        < S : Robot | detected : {S' : Human | (X,Y)} R >
        =>
        < S : Robot | > .   



    ***Rules for when robot has arrived at growingloc and starts a task.
    rl [startWatering] :
        < S : Worker | location : (X,Y), remainingPath : nil, state : idle, cargoLevel : full, curCargo : water > 
        < S2 : GrowingLoc | location : (X,Y), waterLevel : low, growingArea : A >
        =>
        < S : Worker | remainingPath : coverArea((X,Y), A), state : watering, speed : 1 > 
        < S2 : GrowingLoc | > .
    rl [startFertilizing] :
        < S : Worker | location : (X,Y), remainingPath : nil, state : idle, cargoLevel : full, curCargo : fertilizer >
        < S2 : GrowingLoc | location : (X,Y), fertilizerLevel : low, growingArea : A >
        =>
        < S : Worker | remainingPath : coverArea((X,Y), A), state : fertilizing, speed : 1 >
        < S2 : GrowingLoc | > .
    rl [startHarvesting] :
        < S : Transporter | location : (X,Y), remainingPath : nil, state : idle, cargoLevel : empty >
        < S2 : GrowingLoc | location : (X,Y), growingState : harvest, growingArea : A > 
        =>
        < S : Transporter | remainingPath : coverArea((X,Y), A), state : harvesting, speed : 1 >
        < S2 : GrowingLoc | > .
    ***Transporter behavior at collection point.
    rl [startUnloading] :
        < S : Transporter | location : (X,Y), remainingPath : (X,Y), state : transporting, cargoLevel : full >
        < S2 : CPoint | location : (X,Y) >
        =>
        < S : Transporter | remainingPath : nil, state : unloading > 
        < S2 : CPoint | > .

***(
    ***Rules for when a robot finishes a task
    rl [finishWatering] :
        < S : Worker | location : (X,Y), remainingPath : (X,Y), state : watering >
        < S2 : GrowingLoc | location : (X,Y), waterLevel : low >
        =>
        < S : Worker | state : idle, cargoLevel : empty >
        < S2 : GrowingLoc | waterLevel : good, waterTimer : 100 > .
    rl [finishFertilizing] :
        < S : Worker | location : (X,Y), remainingPath : (X,Y), state : fertilizing >
        < S2 : GrowingLoc | location : (X,Y), fertilizerLevel : low >
        =>
        < S : Worker | state : idle, cargoLevel : empty >
        < S2 : GrowingLoc | fertilizerLevel : good, fertilizerTimer : 100 > .
    rl [finishHarvesting] :
        < S : Transporter | location : (X,Y), remainingPath : (X,Y), state : harvesting >
        < S' : GrowingLoc | location : (X,Y), growingState : harvest, content : C >  
        < S2 : CPoint | >
        < S2' : Map | allIntersections : CONF >
        =>
        < S : Transporter | remainingPath : dijkstraSearch(CONF, returnByLocation(CONF, (X,Y)), returnByName(CONF, S2)), speed : 1,  cargoLevel : full, state : transporting >
        < S' : GrowingLoc | growingState : empty >
        < S2 : CPoint | >
        < S2' : Map | > .
    rl [finishUnloading] :
        < S : Transporter | location : (X,Y), remainingPath : nil, state : unloading, curCargo : TC, cargoLevel : full >
        < S2 : CPoint | location : (X,Y), storage : STOR >
        =>
        < S : Transporter | state : idle, cargoLevel : empty >
        < S2 : CPoint | storage : addToStorage(TC, STOR) > .
)***
endom