load robot .
load collectionPoint .
load time-inf .
omod TIMED-FARM is
    protecting ROBOT .
    protecting CPOINT .
    protecting TIME-INF .
    protecting MAP .
    protecting GROWING-LOC .

    sort ClockedConfiguration .
    op timedTest : -> ClockedConfiguration .
    op  _@_ : Configuration Time -> ClockedConfiguration [ctor] .
    
    var CONF : Configuration .
    var T : Time .
    var O : Object .
    var S : String .
    vars X X2 Y Y2 SP : Int .
    var N N' N2 N2' : Nat .
    var LL : List-Location .
    
    ***operators to ignore certain values when checking for maxTimeIncrease, and to ensure that a timer doesn to go to the negative.
    vars I I2 : Int .
    vars NI NI2 : Nat-Inf .
    op minusToZero : Int Int -> Nat .
    eq minusToZero(I, I2) = if (I - I2) < 0 then 0 else I - I2 fi .
    op minIgnoreZero : Nat-Inf Nat-Inf -> Nat-Inf [assoc comm] .
    eq minIgnoreZero(0,0) = Inf .
    eq minIgnoreZero(0, NI) = NI .
    eq minIgnoreZero(NI, NI2) = min(NI, NI2) .

    ***Increases the time for each object in the configuration with the given amount of time.
    op increaseTimeGlobal : Configuration Time -> Configuration .
    eq increaseTimeGlobal(none, T) = none .
    eq increaseTimeGlobal(< S : Map | > CONF, T) = increaseTimeGlobal(CONF, T) < S : Map | > .
    eq increaseTimeGlobal(< S : CPoint | > CONF, T) = increaseTimeGlobal(CONF, T) < S : CPoint | > .
    eq increaseTimeGlobal(< S : Robot | > CONF, T)  = increaseTime(< S : Robot | >, T) increaseTimeGlobal(CONF, T) .
    eq increaseTimeGlobal(< S : Growing-loc | > CONF, T) = increaseTime(< S : Growing-loc | >, T) increaseTimeGlobal(CONF, T) .

    ***Returns a configuration with all objects that are relevant for deciding how much time the config can progress.
    op maxTimeIncreaseHelper : Configuration -> Configuration .
    eq maxTimeIncreaseHelper(none) = none .
    eq maxTimeIncreaseHelper(< S : Map | > CONF) = maxTimeIncreaseHelper(CONF) .
    eq maxTimeIncreaseHelper(< S : CPoint | > CONF) = maxTimeIncreaseHelper(CONF) .
    eq maxTimeIncreaseHelper(< S : Robot | > CONF) = < S : Robot | > maxTimeIncreaseHelper(CONF) .
    eq maxTimeIncreaseHelper(< S : Growing-loc | > CONF) = < S : Growing-loc | > maxTimeIncreaseHelper(CONF) .

    ***Used on a configuration and returns the max permissible time the conf can progress.
    op maxTimeIncreaseGlobal : Configuration -> Time .
    eq maxTimeIncreaseGlobal(none) = inf .
    eq maxTimeIncreaseGlobal(< S : Robot | > CONF) = min(maxTimeIncrease(< S : Robot | >),maxTimeIncreaseGlobal(CONF)) .
    eq maxTimeIncreaseGlobal(< S : Growing-loc | > CONF) = min(maxTimeIncrease(< S : Growing-loc | >),maxTimeIncreaseGlobal(CONF)) .

    op increaseTime : Object Nat -> Object .
    op maxTimeIncrease : Object -> Time .
    ***maxTimeIncrease on robots returns a natural number based on how many times the robot can move until it reaches the next intersection in its path
    ***Ignore robots that are finished with their path.
    eq maxTimeIncrease(< S : Robot | remainingPath : nil >) = inf .
    eq maxTimeIncrease(< S : Robot | location : (X, Y), speed : SP, remainingPath : (X2, Y2) LL >) = distance((X,Y), (X2,Y2)) / SP  .
    ***maxTimeIncrease for growing-loc is the amount of time until one of the timer reaches 0.
    ***If the timer has reached 0, but the state hasnt changed yet, maxTimeIncrease is 0, so that the state can be updated.
    eq maxTimeIncrease(< S : Growing-loc | waterTimer : 0, waterLevel : good >) = 0 .
    eq maxTimeIncrease(< S : Growing-loc | fertilizerTimer : 0, fertilizerLevel : good >) = 0 .
    eq maxTimeIncrease(< S : Growing-loc | growTimer : 0, state : growing >) = 0 .
    eq maxTimeIncrease(< S : Growing-loc | waterTimer : N, fertilizerTimer : N', growTimer : N2 >) = minIgnoreZero(N2,minIgnoreZero(N,N')) .

    ***Uses an if to ensure that the robot doesnt move past its destination.
    eq increaseTime(< S : Robot | remainingPath : nil >, N)
        =
        < S : Robot | > .
    eq increaseTime(< S : Robot | location : (X , Y), direction : north, speed : SP, remainingPath : (X2,Y2) LL >, N)
        =
        if Y + (SP * N) > Y2
        then
            < S : Robot | location : (X , Y2) > 
        else
            < S : Robot | location : (X, Y + (N * SP)) >  
        fi .
    eq increaseTime(< S : Robot | location : (X , Y), direction : east, speed : SP, remainingPath : (X2,Y2) LL >, N)
        =
        if X + (SP * N) > X2
        then
            < S : Robot | location : (X2 , Y) >
        else
            < S : Robot | location : (X + (SP * N), Y) >
        fi .
    eq increaseTime(< S : Robot | location : (X , Y), direction : south, speed : SP, remainingPath : (X2,Y2) LL >, N)
        =
        if Y - (SP * N) < Y2
        then
            < S : Robot | location : (X, Y2) >
        else
            < S : Robot | location : (X, Y - (SP * N)) >
        fi .
    eq increaseTime(< S : Robot | location : (X , Y), direction : west, speed : SP, remainingPath : (X2, Y2) LL >, N)
        =
        if X - (SP * N) < X2
        then
            < S : Robot | location : (X2, Y) >
        else
            < S : Robot | location : (X - (SP * N), Y) >
        fi .

    eq increaseTime(< S : Growing-loc | waterLevel : low, fertilizerLevel : low >, N) = < S : Growing-loc | > .
    eq increaseTime(< S : Growing-loc | waterLevel : low, fertilizerTimer : N >, N2) = < S : Growing-loc | fertilizerTimer : minusToZero(N,N2) > .
    eq increaseTime(< S : Growing-loc | fertilizerLevel : low, waterTimer : N >, N2) = < S : Growing-loc | waterTimer : minusToZero(N,N2) > . 
    eq increaseTime(< S : Growing-loc | waterTimer : N, fertilizerTimer : N', growTimer : N2, state : harvest >, N2') =
        < S : Growing-loc | waterTimer : minusToZero(N,N2'), fertilizerTimer : minusToZero(N', N2'), growTimer : minusToZero(N2, N2') > .
    eq increaseTime(< S : Growing-loc | waterTimer : N, fertilizerTimer : N', growTimer : N2 >, N2') =
        < S : Growing-loc | waterTimer : minusToZero(N,N2'), fertilizerTimer : minusToZero(N', N2'), growTimer : minusToZero(N2, N2') > .

    crl [increaseTime] :
        CONF @ T
        =>
        increaseTimeGlobal(CONF, maxTimeIncreaseGlobal(maxTimeIncreaseHelper(CONF))) @ (T + maxTimeIncreaseGlobal(maxTimeIncreaseHelper(CONF)))
        if maxTimeIncreaseGlobal(maxTimeIncreaseHelper(CONF)) > 0 .
endom

