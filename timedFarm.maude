load robot .
load collectionPoint .
load time-inf .
load obstacle . 

omod TIMED-FARM is
    protecting ROBOT .
    protecting CPOINT .
    protecting TIME-INF .
    protecting MAP .
    protecting GROWING-LOC .
    protecting OBSTACLE .

    sort ClockedConfiguration .

    op  _@_ : Configuration Time -> ClockedConfiguration [ctor] .
    op minusToZero : Int Int -> Nat .
    op minIgnoreZero : NatInf NatInf -> NatInf [assoc comm] .
    op maxTimeAdvance : Configuration -> Time .
    op timeEffect : Object Time -> Object .
    op calculateAdvance : Object Object -> Time [comm] .
    op collision : Object Object -> Configuration [comm] .
    op handleCollisions : Configuration -> Configuration .
    op updateLocation : Object -> Object .
    op detect : Configuration -> Configuration .

    vars CONF CONF2 CONF3 : Configuration .
    var OBS : Configuration .
    var T : Time .
    var O : Object .
    vars S S2 : String .
    vars X X2 Y Y2 X3 Y3 SP SP' : Int .
    vars N N' N2 N3 N4 : Nat .
    var LL : List-Location .
    vars I I2 : Int .
    vars NI NI2 : NatInf .
    vars D D' : Direction .
    var R : Representation .
    var C : Cid .

    eq minusToZero(I, I2) = if (I - I2) < 0 then 0 else I - I2 fi .
    eq minIgnoreZero(0,0) = natInf .
    eq minIgnoreZero(0, NI) = NI .
    eq minIgnoreZero(NI, NI2) = min(NI, NI2) .

    eq updateLocation(< S : Entity | location : (X,Y), direction : north >) = < S : Entity | location : (X, Y - 1) > .
    eq updateLocation(< S : Entity | location : (X,Y), direction : east >) = < S : Entity | location : (X - 1, Y) > .
    eq updateLocation(< S : Entity | location : (X,Y), direction : south >) = < S : Entity | location : (X, Y + 1) > .
    eq updateLocation(< S : Entity | location : (X,Y), direction : west >) = < S : Entity | location : (X + 1, Y) > .

    eq collision(< S : Entity | location : (X,Y) >, < S2 : Obstacle | >) =  updateLocation(< S : Entity | speed : 0, state : crashed >) < S2 : Obstacle | > .
    ceq collision(< S : Entity | speed : SP >, < S2 : Entity | speed : SP' >) = updateLocation(< S : Entity | speed : 0, state : crashed >) < S2 : Entity | speed : 0, state : crashed > if SP >= SP' .

    eq handleCollisions(< S : Entity | location : (X,Y) > < S2 : PhysicalObj | location : (X,Y) > CONF) = collision(< S : Entity | >, < S2 : PhysicalObj | >) handleCollisions(CONF) .
    eq handleCollisions(CONF) = CONF [owise] .

    eq calculateAdvance(< S : Entity | location : (X,Y), speed : SP, remainingPath : (X2,Y2) LL >, none) = distance((X,Y), (X2,Y2)) / SP .
    ceq calculateAdvance(< S : Entity | location : (X,Y), speed : SP, direction : D >, < S2 : Entity | location : (X2,Y2), speed : SP', direction : D' >) = distance((X,Y), (X2,Y2)) / (SP + SP') if (D == north and D' == south) or (D == east and D' == west) .
    eq calculateAdvance(< S : Entity | location : (X,Y), speed : SP >, < S2 : Obstacle | location : (X2,Y2) >) = distance((X,Y), (X2,Y2)) / SP [owise] .

    eq maxTimeAdvance(< S : Map | > CONF) = maxTimeAdvance(CONF) .
    eq maxTimeAdvance(CONF) = inf [owise] .

    ***maxTimeAdvance for growing-loc is the amount of time until one of the timers reaches 0.
    ***If the timer has reached 0, but the state hasn't changed yet, maxTimeAdvance is 0, so that the state can be updated.
    ***maxTimeAdvance of growing-loc does not depend on the rest of the environment.
    eq maxTimeAdvance(< S : Growing-loc | waterTimer : 0, waterLevel : good > CONF) = 0 .
    eq maxTimeAdvance(< S : Growing-loc | fertilizerTimer : 0, fertilizerLevel : good > CONF) = 0 .
    eq maxTimeAdvance(< S : Growing-loc | growTimer : 0, growingState : growing > CONF) = 0 .
    eq maxTimeAdvance(< S : Growing-loc | waterTimer : N, fertilizerTimer : N', growTimer : N2 > CONF) = min(minIgnoreZero(N2,minIgnoreZero(N,N')), maxTimeAdvance(CONF)) .

    ceq maxTimeAdvance(< S : Robot | detected : R > CONF) = 0 if R =/= nil .

    eq maxTimeAdvance(< S : Entity | remainingPath : nil > CONF) = maxTimeAdvance(CONF) .
    eq maxTimeAdvance(< S : Entity | state : crashed > CONF) = maxTimeAdvance(CONF) .
    eq maxTimeAdvance(< S : Entity | location : (X, Y), speed : SP, remainingPath : (X2, Y2) LL > CONF)
    =
    min(calculateAdvance(< S : Entity | >, obstacleInWay((X,Y), (X2,Y2), CONF)), maxTimeAdvance(CONF)) .

    ***call maxTimeAdvance on every object in the configuration.
    eq timeEffect(none, N) = none .
    ***call timeEffect on every object in the configuration.
    eq timeEffect(< S : Map | > CONF, N) = < S : Map | > timeEffect(CONF, N) .
    eq timeEffect(< S : Obstacle | > CONF, N) = < S : Obstacle | > timeEffect(CONF, N) .
    ***Entities move in the direction depending on how much time passes and their speed.
    eq timeEffect(< S : Entity | remainingPath : nil > CONF, N) = < S : Entity | > timeEffect(CONF, N) .
    eq timeEffect(< S : Entity | state : crashed > CONF, N) = < S : Entity | > timeEffect(CONF,N) .
    eq timeEffect(< S : Entity | location : (X,Y), direction : north, speed : SP, remainingPath : (X2,Y2) LL > CONF, N) = < S : Entity | location : (X, Y + (SP * N)) > timeEffect(CONF, N) .
    eq timeEffect(< S : Entity | location : (X,Y), direction : east, speed : SP, remainingPath : (X2,Y2) LL > CONF, N) = < S : Entity | location : (X + (SP * N), Y) > timeEffect(CONF, N) .
    eq timeEffect(< S : Entity | location : (X,Y), direction : south, speed : SP, remainingPath : (X2,Y2) LL > CONF, N) = < S : Entity | location : (X, Y - (SP * N)) > timeEffect(CONF, N) .
    eq timeEffect(< S : Entity | location : (X,Y), direction : west, speed : SP, remainingPath : (X2,Y2) LL > CONF, N) = < S : Entity | location : (X - (SP * N), Y) > timeEffect(CONF, N) .

    eq timeEffect(< S : Growing-loc | waterLevel : low, fertilizerLevel : low > CONF, N) = < S : Growing-loc | > timeEffect(CONF, N) .
    eq timeEffect(< S : Growing-loc | waterLevel : low, fertilizerTimer : N > CONF, N2) = < S : Growing-loc | fertilizerTimer : minusToZero(N,N2) > timeEffect(CONF, N2) .
    eq timeEffect(< S : Growing-loc | fertilizerLevel : low, waterTimer : N > CONF, N2) = < S : Growing-loc | waterTimer : minusToZero(N,N2) > timeEffect(CONF, N2) . 
    eq timeEffect(< S : Growing-loc | waterTimer : N, fertilizerTimer : N', growTimer : N2, growingState : harvest > CONF, N3) =
        < S : Growing-loc | waterTimer : minusToZero(N,N3), fertilizerTimer : minusToZero(N', N3), growTimer : minusToZero(N2, N3) > timeEffect(CONF, N3) .
    eq timeEffect(< S : Growing-loc | waterTimer : N, fertilizerTimer : N', growTimer : N2 > CONF, N3) =
        < S : Growing-loc | waterTimer : minusToZero(N,N3), fertilizerTimer : minusToZero(N', N3), growTimer : minusToZero(N2, N3) > .

    ceq detect(< S : Robot | location : (X,Y), detectionRange : N, detected : R > < S2 : PhysicalObj | location : (X2,Y2) > CONF) =
        detect(< S : Robot | detected : {S2 : PhysicalObj | (X2,Y2)} R > < S2 : PhysicalObj | > CONF) if ((X2,Y2) inArea(makeArea((X,Y), N))) and S2 notIn R .
    eq detect(CONF) = CONF [owise] .

    crl [timeEffect] :
        CONF @ T
        =>
        handleCollisions(timeEffect(CONF, maxTimeAdvance(CONF))) @ (T + maxTimeAdvance(CONF))
        if maxTimeAdvance(CONF) > 0 and maxTimeAdvance(CONF) < inf .
endom