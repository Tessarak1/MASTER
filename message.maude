load locatable .
load task .
load time-inf .
load negotiation .
load systemVars .
***Code for message-wrapper is taken from assignment 3 regarding sliding window in maude.
omod MESSAGE-WRAPPER is
  protecting OID-SET .

  sorts MsgContent MsgContentSet .

  subsort MsgContent < MsgContentSet .

  op noContent : -> MsgContentSet [ctor] .
  op _&_ : MsgContentSet MsgContentSet -> MsgContentSet [assoc comm ctor id: noContent] .
  op msg_from_to_ : MsgContentSet Oid Oid -> Msg [ctor] .
endom
 
omod MESSAGE is
  protecting MESSAGE-WRAPPER .
  protecting COORDINATE-LIST .
  protecting TASK-MSET .
  protecting TIME-INF .
  protecting NEGOTIATION .
  
  sort DlyMsg .
  
  subsort Msg < DlyMsg < Configuration .
  subsort Tid < MsgContent .

  op dly : Msg Time -> DlyMsg [ctor right id: 0] .
  ops obstacleAt_ removed_ yielding_ : Coordinate -> MsgContent .
  ops path conflictYield : List-Coordinate -> MsgContent .
  op pathCost : Cost -> MsgContent .
  ops requestPath noConflict completedYield noYield : -> MsgContent .

  ***Converts a set of tasks into msg content set.
  op tidToMsgContent : MSet{TidElt} -> MsgContentSet .

  var TID : Tid .
  var TIDS : MSet{TidElt} .

  eq tidToMsgContent(empty) = noContent .
  eq tidToMsgContent(TID TIDS) = TID & tidToMsgContent(TIDS) .
endom

omod MESSENGER is
  protecting MESSAGE .
  protecting LOCATABLE .
  protecting SYSTEM-VARS .

  ***Objects of this class can communicate with one another.
  ***Each object has a range that they can communicate within. 
  class Messenger | broadcastRange : Rat .
  
  subclass Messenger < Locatable .

  ***Creates a message from Oid to everyone in the Set{OidElt}.
  op broadcast : Oid MsgContent Set{OidElt} -> Configuration .

  ***Broadcasts messages from Object to each object in the configuration that is in range.
  op broadcastInRange : Object MsgContent Configuration -> Configuration .
  op $broadcastInRange : Oid MsgContent Set{OidElt} -> Configuration .

  ***returns the oids of messengers.
  op messengerOids : Configuration -> Set{OidElt} .

  vars OID OID2 : Oid .
  var OIDS : Set{OidElt} .
  var R : Rat .
  var MC : MsgContent .
  var MCS : MsgContentSet .
  var CONF : Configuration .
  var TIDS : MSet{TidElt} .

  eq messengerOids(< OID : Messenger | > CONF) = (OID, messengerOids(CONF)) .
  eq messengerOids(CONF) = empty [owise] .

  eq broadcast(OID, MC, empty) = none .
  eq broadcast(OID, MC, (OID2, OIDS)) = dly((msg MC from OID to OID2), MSG-DELAY) broadcast(OID, MC, OIDS) .

  eq broadcastInRange(< OID : Messenger | >, noContent, CONF) = none .
  eq broadcastInRange(< OID : Messenger | broadcastRange : R >, MCS, CONF) =
    $broadcastInRange(OID, MCS, messengerOids(withinDistance(< OID : Messenger | >, R, CONF))) .
  
  eq $broadcastInRange(OID, noContent, OIDS) = none .
  eq $broadcastInRange(OID, MC & MCS, OIDS) = broadcast(OID, MC, OIDS) $broadcastInRange(OID, MCS, OIDS) .
endom
