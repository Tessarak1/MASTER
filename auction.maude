load task .
load timer .
load oidSet .
load cost .
load systemVars . 

fmod AUCTION-SORTS is
    sort Bid .
    sort Auction .
endfm

view BidElt from TRIV to AUCTION-SORTS is
    sort Elt to Bid .
endv

view AuctionElt from TRIV to AUCTION-SORTS is
    sort Elt to Auction .
endv

omod AUCTION is
    protecting TIMER-MSET .
    protecting TASK-MSET .
    protecting COST .
    protecting SYSTEM-VARS .
    protecting OID-SET .
    protecting SET{AuctionElt} .
    protecting SET{BidElt} .

    op [_bids_] : Oid Cost -> Bid [ctor] .
    op [Task:__|_] : Tid Timer Set{BidElt} -> Auction [ctor] .
    op auctionWinner : Set{BidElt} Oid Cost -> Oid .
    op createAuction : Tid -> Auction .
    op auctionExists : Tid Set{AuctionElt} -> Bool .
    op closed : Tid Set{AuctionElt} -> Bool .
    op bidders : Set{BidElt} -> Set{OidElt} .
    op getTimers : Set{AuctionElt} -> MSet{TimerElt} .
    op decTimers : Set{AuctionElt} Time -> Set{AuctionElt} .

    var TID : Tid .
    var TR : Timer .
    var BS : Set{BidElt} .
    var AUS : Set{AuctionElt} .
    vars OID OID2 : Oid .
    var COST : Cost .
    vars RI RI2 : RatInf .
    vars TIME TIME2 TIME3 : Time .

    eq auctionWinner(empty, OID, COST) = OID .
    eq auctionWinner(([OID bids cost(RI)], BS), 
            OID2, 
            cost(RI2)) 
        =
        if RI < RI2
        then
            auctionWinner(BS, OID, cost(RI))
        else
            auctionWinner(BS, OID2, cost(RI2))
        fi .

    eq createAuction(TID) = 
        [Task: TID AUCTION-TIMER | empty] .

    eq closed(TID, ([Task: TID TR | BS], AUS)) = false .
    eq closed(TID, AUS) = true [owise] .

    eq bidders(empty) = empty .
    eq bidders(([OID bids COST], BS)) 
        = 
        (OID, bidders(BS)) .

    eq getTimers(empty) = empty .
    eq getTimers(([Task: TID TR | BS], AUS)) = TR getTimers(AUS) .

    eq decTimers(empty, TIME) = empty .
    eq decTimers(([Task: TID TR | BS], AUS), TIME) =
        ([Task: TID dec(TR, TIME) | BS],
        decTimers(AUS, TIME)) .

    eq auctionExists(TID, ([Task: TID TR | BS] , AUS)) = true .
    eq auctionExists(TID, AUS) = false [owise] .
endom
